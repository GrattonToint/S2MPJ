***************************
* SET UP THE INITIAL DATA *
***************************

NAME          ROSEPETAL

*   Problem :
*   --------

*   An ammusement: minimize sum_i i x_i 
*                  subject to ( x_j +/- 1 )^2 + sum_i/=j x_i^2 <= r^2 (1<=j<=n)
*   where r >= 1

*   SIF input: Nick Gould, Feb 2014

*   classification LQR2-AN-V-V

*   Number of variables

*IE N                   2              $-PARAMETER
*IE N                   10             $-PARAMETER
*IE N                   100            $-PARAMETER
*IE N                   1000           $-PARAMETER
 IE N                   2              $-PARAMETER     modified for S2X tests
*IE N                   10000          $-PARAMETER
*IE N                   100000         $-PARAMETER

*  Constraint radius >= 1

*RE R                   1.0            $-PARAMETER
 RE R                   2.0            $-PARAMETER

*   other parameter definitions

 IE 1                   1
 R* R2        R                        R
 RA R2-1      R2        -1.0

VARIABLES

 DO I         1                        N
 X  X(I)
 ND

GROUPS

 DO I         1                        N
 RI RI        I
 ZN OBJ       X(I)                     RI
 XL M(I)      X(I)      -2.0
 XL P(I)      X(I)      2.0
 ND

CONSTANTS

 DO I         1                        N
 Z  ROSEPETAL M(I)                     R2-1
 Z  ROSEPETAL P(I)                     R2-1
 ND

BOUNDS

 FR ROSEPETAL 'DEFAULT'

START POINT

*  start with x = r^2

 ZV ROSEPETAL 'DEFAULT'                R2

ELEMENT TYPE

 EV SQR       V

ELEMENT USES

 XT 'DEFAULT' SQR

 DO I         1                        N
 ZV Q(I)      V                        X(I)
 ND

GROUP USES

 DO I         1                        N
 DO J         1                        N
 XE M(I)      Q(J)
 XE P(I)      Q(J)
 ND

ENDATA

***********************
* SET UP THE FUNCTION *
* AND RANGE ROUTINES  *
***********************

ELEMENTS      ROSEPETAL

INDIVIDUALS

 T  SQR
 F                      V ** 2
 G  V                   2.0D+0 * V
 H  V         V         2.0D+0

ENDATA

