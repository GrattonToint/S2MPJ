***************************
* SET UP THE INITIAL DATA *
***************************

NAME          SPMSRTLS

*   Problem :
*   *********

*   Liu and Nocedal tridiagonal matrix square root problem.

*   Source:  problem 151 (p. 93) in
*   A.R. Buckley,
*   "Test functions for unconstrained minimization",
*   TR 1989CS-3, Mathematics, statistics and computing centre,
*   Dalhousie University, Halifax (CDN), 1989.

*   This is a least-squares variant of problem SPMSQRT.

*   SIF input: Ph. Toint, Dec 1989.

*   classification SUR2-AN-V-V

*   M is the dimension of the matrix
*   The number of variables is 3*M-2

*IE M                   10             $-PARAMETER n = 28     original value
*IE M                   34             $-PARAMETER n = 100
*IE M                   167            $-PARAMETER n = 499
*IE M                   334            $-PARAMETER n = 1000
 IE M                   1667           $-PARAMETER n = 4999
*IE M                   3334           $-PARAMETER n = 10000

*   Other parameters

 IA M-1       M         -1
 IA M-2       M         -2
 IA M-3       M         -3
 IE 1                   1
 IE 2                   2
 IE 3                   3
 IE 4                   4

*  Set up the entries for the matrix B.

*  Row 1.

 RF B1,1      SIN       1.0
 RF B1,2      SIN       4.0

*  Rows 2 to M-1.

 IE K                   2

 DO I         2                        M-1

 IA I-1       I         -1
 IA I+1       I         1
 IA K         K         1
 RI RK        K
 R* RKSQR     RK                       RK
 A( B(I,I-1)  SIN                      RKSQR
 IA K         K         1
 RI RK        K
 R* RKSQR     RK                       RK
 A( B(I,I)    SIN                      RKSQR
 IA K         K         1
 RI RK        K
 R* RKSQR     RK                       RK
 A( B(I,I+1)  SIN                      RKSQR

 ND

*  Row M.

 IA K         K         1
 RI RK        K
 R* RKSQR     RK                       RK
 A( B(M,M-1)  SIN                      RKSQR
 IA K         K         1
 RI RK        K
 R* RKSQR     RK                       RK
 A( B(M,M)    SIN                      RKSQR

VARIABLES

 X  X(1,1)
 X  X(1,2)

 DO I         2                        M-1

 IA I-1       I         -1
 IA I+1       I         1

 X  X(I,I-1)
 X  X(I,I)
 X  X(I,I+1)

 ND

 X  X(M,M-1)
 X  X(M,M)

GROUPS

*  Each group corresponds to an entry in the pentadiagonal matrix A.
*  The difference between the entries of A and X(trans) X is group E.

 DO J         1                        3
 XN E(1,J)
 ND

 DO J         1                        4
 XN E(2,J)
 ND

 DO I         3                        M-2
 IA I-2       I         -2
 IA I+2       I         2
 DO J         I-2                      I+2
 XN E(I,J)
 ND

 DO J         M-3                      M
 XN E(M-1,J)
 ND

 DO J         M-2                      M
 XN E(M,J)
 ND

CONSTANTS

*  Form the first diagonal of A.

 R* ENTRY     B1,1                     B1,1
 R* PROD      B1,2                     B2,1
 R+ ENTRY     ENTRY                    PROD
 Z  SPMSRTLS  E1,1                     ENTRY

*  Form diagonals 2 to M-1 of A.

 DO I         2                        M-1
 IA I-1       I         -1
 IA I+1       I         1
 A* ENTRY     B(I,I)                   B(I,I)
 A* PROD      B(I-1,I)                 B(I,I-1)
 A+ ENTRY     ENTRY                    PROD
 A* PROD      B(I+1,I)                 B(I,I+1)
 A+ ENTRY     ENTRY                    PROD
 Z  SPMSRTLS  E(I,I)                   ENTRY
 ND

*  Form the last diagonal of A.

 A* ENTRY     B(M,M)                   B(M,M)
 A* PROD      B(M-1,M)                 B(M,M-1)
 A+ ENTRY     ENTRY                    PROD
 Z  SPMSRTLS  E(M,M)                   ENTRY

*  Form sub-diagonals 1 to M-1 of A.

 DO I         1                        M-1
 IA I+1       I         1
 A* ENTRY     B(I+1,I)                 B(I,I)
 A* PROD      B(I+1,I+1)               B(I+1,I)
 A+ ENTRY     ENTRY                    PROD
 Z  SPMSRTLS  E(I+1,I)                 ENTRY
 ND

*  Form super-diagonals 2 to M of A.

 DO I         2                        M
 IA I-1       I         -1
 A* ENTRY     B(I-1,I)                 B(I,I)
 A* PROD      B(I-1,I-1)               B(I-1,I)
 A+ ENTRY     ENTRY                    PROD
 Z  SPMSRTLS  E(I-1,I)                 ENTRY
 ND

*  Form sub-sub-diagonals 2 to M-1 of A.

 DO I         2                        M-1
 IA I-1       I         -1
 IA I+1       I         1
 A* ENTRY     B(I+1,I)                 B(I,I-1)
 Z  SPMSRTLS  E(I+1,I-1)               ENTRY
 ND

*  Form super-super-diagonals 2 to M-1 of A.

 DO I         2                        M-1
 IA I-1       I         -1
 IA I+1       I         1
 A* ENTRY     B(I-1,I)                 B(I,I+1)
 Z  SPMSRTLS  E(I-1,I+1)               ENTRY
 ND

BOUNDS

 FR SPMSRTLS  'DEFAULT'

START POINT

*  The starting point is 0.2 B.

 RM PROD      B1,1      0.2
 Z  SPMSRTLS  X1,1                     PROD
 RM PROD      B1,2      0.2
 Z  SPMSRTLS  X1,2                     PROD

 DO I         2                        M-1
 IA I-1       I         -1
 IA I+1       I         1
 AM PROD      B(I,I-1)  0.2
 Z  SPMSRTLS  X(I,I-1)                 PROD
 AM PROD      B(I,I)    0.2
 Z  SPMSRTLS  X(I,I)                   PROD
 AM PROD      B(I,I+1)  0.2
 Z  SPMSRTLS  X(I,I+1)                 PROD

 ND

 AM PROD      B(M,M-1)  0.2
 Z  SPMSRTLS  X(M,M-1)                 PROD
 AM PROD      B(M,M)    0.2
 Z  SPMSRTLS  X(M,M)                   PROD

ELEMENT TYPE

 EV PROD2     VI                       VJ
 EV SQ        V

ELEMENT USES

*  Line 1 of the squared matrix

 XT D(1)      SQ
 ZV D(1)      V                        X(1,1)

 XT G(1)      PROD2
 ZV G(1)      VI                       X(1,2)
 ZV G(1)      VJ                       X(2,1)

 XT H(1)      PROD2
 ZV H(1)      VI                       X(1,1)
 ZV H(1)      VJ                       X(1,2)

 XT R(1)      PROD2
 ZV R(1)      VI                       X(1,2)
 ZV R(1)      VJ                       X(2,2)

 XT S(1)      PROD2
 ZV S(1)      VI                       X(1,2)
 ZV S(1)      VJ                       X(2,3)

*  Line 2 of the squared matrix

 XT B(2)      PROD2
 ZV B(2)      VI                       X(2,1)
 ZV B(2)      VJ                       X(1,1)

 XT C(2)      PROD2
 ZV C(2)      VI                       X(2,2)
 ZV C(2)      VJ                       X(2,1)

 XT F(2)      PROD2
 ZV F(2)      VI                       X(2,1)
 ZV F(2)      VJ                       X(1,2)

 XT D(2)      SQ
 ZV D(2)      V                        X(2,2)

 XT G(2)      PROD2
 ZV G(2)      VI                       X(2,3)
 ZV G(2)      VJ                       X(3,2)

 XT H(2)      PROD2
 ZV H(2)      VI                       X(2,2)
 ZV H(2)      VJ                       X(2,3)

 XT R(2)      PROD2
 ZV R(2)      VI                       X(2,3)
 ZV R(2)      VJ                       X(3,3)

 XT S(2)      PROD2
 ZV S(2)      VI                       X(2,3)
 ZV S(2)      VJ                       X(3,4)

*  Line i of the squared matrix 2 < i < m-1

 DO I         3                        M-2

 IA I-2       I         -2
 IA I-1       I         -1
 IA I+1       I         1
 IA I+2       I         2

 XT A(I)      PROD2
 ZV A(I)      VI                       X(I,I-1)
 ZV A(I)      VJ                       X(I-1,I-2)

 XT B(I)      PROD2
 ZV B(I)      VI                       X(I,I-1)
 ZV B(I)      VJ                       X(I-1,I-1)

 XT C(I)      PROD2
 ZV C(I)      VI                       X(I,I)
 ZV C(I)      VJ                       X(I,I-1)

 XT F(I)      PROD2
 ZV F(I)      VI                       X(I,I-1)
 ZV F(I)      VJ                       X(I-1,I)

 XT D(I)      SQ
 ZV D(I)      V                        X(I,I)

 XT G(I)      PROD2
 ZV G(I)      VI                       X(I,I+1)
 ZV G(I)      VJ                       X(I+1,I)

 XT H(I)      PROD2
 ZV H(I)      VI                       X(I,I)
 ZV H(I)      VJ                       X(I,I+1)

 XT R(I)      PROD2
 ZV R(I)      VI                       X(I,I+1)
 ZV R(I)      VJ                       X(I+1,I+1)

 XT S(I)      PROD2
 ZV S(I)      VI                       X(I,I+1)
 ZV S(I)      VJ                       X(I+1,I+2)

 ND

*  Line M-1 of the squared matrix

 XT A(M-1)    PROD2
 ZV A(M-1)    VI                       X(M-1,M-2)
 ZV A(M-1)    VJ                       X(M-2,M-3)

 XT B(M-1)    PROD2
 ZV B(M-1)    VI                       X(M-1,M-2)
 ZV B(M-1)    VJ                       X(M-2,M-2)

 XT C(M-1)    PROD2
 ZV C(M-1)    VI                       X(M-1,M-1)
 ZV C(M-1)    VJ                       X(M-1,M-2)

 XT F(M-1)    PROD2
 ZV F(M-1)    VI                       X(M-1,M-2)
 ZV F(M-1)    VJ                       X(M-2,M-1)

 XT D(M-1)    SQ
 ZV D(M-1)    V                        X(M-1,M-1)

 XT G(M-1)    PROD2
 ZV G(M-1)    VI                       X(M-1,M)
 ZV G(M-1)    VJ                       X(M,M-1)

 XT H(M-1)    PROD2
 ZV H(M-1)    VI                       X(M-1,M-1)
 ZV H(M-1)    VJ                       X(M-1,M)

 XT R(M-1)    PROD2
 ZV R(M-1)    VI                       X(M-1,M)
 ZV R(M-1)    VJ                       X(M,M)

*  Line M of the squared matrix

 XT A(M)      PROD2
 ZV A(M)      VI                       X(M,M-1)
 ZV A(M)      VJ                       X(M-1,M-2)

 XT B(M)      PROD2
 ZV B(M)      VI                       X(M,M-1)
 ZV B(M)      VJ                       X(M-1,M-1)

 XT C(M)      PROD2
 ZV C(M)      VI                       X(M,M)
 ZV C(M)      VJ                       X(M,M-1)

 XT F(M)      PROD2
 ZV F(M)      VI                       X(M,M-1)
 ZV F(M)      VJ                       X(M-1,M)

 XT D(M)      SQ
 ZV D(M)      V                        X(M,M)

GROUP TYPE

 GV L2        GVAR

GROUP USES

 XT 'DEFAULT' L2

*  Line 1 of the squared matrix

 XE E(1,1)    D(1)                     G(1)
 XE E(1,2)    H(1)                     R(1)
 XE E(1,3)    S(1)

*  Line 2 of the squared matrix

 XE E(2,1)    B(2)                     C(2)
 XE E(2,2)    F(2)
 XE E(2,2)    D(2)                     G(2)
 XE E(2,3)    H(2)                     R(2)
 XE E(2,4)    S(2)


*  Line i of the squared matrix 2 < i < m-1

 DO I         3                        M-2
 IA I-2       I         -2
 IA I-1       I         -1
 IA I+1       I         1
 IA I+2       I         2
 XE E(I,I-2)  A(I)
 XE E(I,I-1)  B(I)                     C(I)
 XE E(I,I)    F(I)
 XE E(I,I)    D(I)                     G(I)
 XE E(I,I+1)  H(I)                     R(I)
 XE E(I,I+2)  S(I)
 ND

*  Line M-1 of the squared matrix

 XE E(M-1,M-3)A(M-1)
 XE E(M-1,M-2)B(M-1)                   C(M-1)
 XE E(M-1,M-1)F(M-1)
 XE E(M-1,M-1)D(M-1)                   G(M-1)
 XE E(M-1,M)  H(M-1)                   R(M-1)

*  Line M of the squared matrix

 XE E(M,M-2)  A(M)
 XE E(M,M-1)  B(M)                     C(M)
 XE E(M,M)    F(M)                     D(M)

ENDATA

***********************
* SET UP THE FUNCTION *
* AND RANGE ROUTINES  *
***********************

ELEMENTS      SPMSRTLS

INDIVIDUALS

 T  PROD2
 F                      VI * VJ
 G  VI                  VJ
 G  VJ                  VI
 H  VI        VJ        1.0

 T  SQ
 F                      V * V
 G  V                   V + V
 H  V         V         2.0

ENDATA


*********************
* SET UP THE GROUPS *
* ROUTINE           *
*********************

GROUPS        SPMSRTLS

INDIVIDUALS

 T  L2
 F                      GVAR * GVAR
 G                      GVAR + GVAR
 H                      2.0

ENDATA
