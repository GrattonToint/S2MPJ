***************************
* SET UP THE INITIAL DATA *
***************************

NAME          DTOC3 

*   Problem :
*   *********

*   This is a discrete time optimal control (DTOC) problem.  
*   The system has N time periods, 1 control variable and 2 state variables.

*   The problem is convex.

*   Sources: problem 3 in
*   T.F. Coleman and A. Liao,
*   "An Efficient Trust Region Method for Unconstrained Discret-Time Optimal
*   Control Problems",
*   Tech. Report, ctc93tr144,  Advanced Computing Research Institute, 
*   Cornell University, 1992.

*   D.P. Bertsekas,
*   "Projected Newton methods for optimization problems with simple
*   constraints", 
*   SIAM J. Control and Optimization 20, pp. 221-246, 1982.

*   SIF input: Ph. Toint, August 1993

*   classification QLR2-AN-V-V

*   Problem variants: they are identified by the value of the parameter N.

*   The problem has 3N-1  variables (of which 2 are fixed),
*   and 2(N-1) constraints

*IE N                   10             $-PARAMETER  n=   29,m= 18 original value
*IE N                   50             $-PARAMETER  n=  149,m= 98
*IE N                   100            $-PARAMETER  n=  299,m=198
*IE N                   500            $-PARAMETER  n= 1499,m=998
*IE N                   1000           $-PARAMETER  n= 2999,m=1998
*IE N                   1500           $-PARAMETER  n= 4499,m=2998
 IE N                   10             $-PARAMETER     modified for S2X tests
*IE N                   5000           $-PARAMETER  n=14999,m=9998

*   Constants

 IA N-1       N         -1
 IE 1                   1
 IE 2                   2
 RI RN        N
 RD S         RN        1.0
 RD 2/S       S         2.0
 RM -S        S         -1.0

VARIABLES

 DO T         1                        N-1
 X  X(T)
 OD T

 DO T         1                        N
 DO I         1                        2
 X  Y(T,I)
 OD I
 OD T

GROUPS

*   Objective function

 DO T         1                        N-1
 ZN O(T)      'SCALE'                  2/S
 OD T

*   Transition constraints

 DO T         1                        N-1
 IA T+1       T         1

 XE TT(T,1)   Y(T+1,1)  -1.0           Y(T,1)    1.0
 ZE TT(T,1)   Y(T,2)                   S

 XE TT(T,2)   Y(T+1,2)  -1.0           Y(T,2)    1.0
 ZE TT(T,2)   Y(T,1)                   -S
 ZE TT(T,2)   X(T)                     S

 OD T

BOUNDS

 FR DTOC3     'DEFAULT'

 XX DTOC3     Y(1,1)    15.0
 XX DTOC3     Y(1,2)    5.0


START POINT


 XV DTOC3     Y(1,1)    15.0
 XV DTOC3     Y(1,2)    5.0

ELEMENT TYPE

 EV SQ        YY

ELEMENT USES

 DO T         2                        N
 XT Y1SQ(T)   SQ
 ZV Y1SQ(T)   YY                       Y(T,1)
 XT Y2SQ(T)   SQ
 ZV Y2SQ(T)   YY                       Y(T,2)
 OD T

 DO T         1                        N-1
 XT XSQ(T)    SQ
 ZV XSQ(T)    YY                       X(T)
 OD T

GROUP USES

 DO T         1                        N-1
 IA T+1       T         1
 XE O(T)      Y1SQ(T+1) 2.0            Y2SQ(T+1) 1.0
 XE O(T)      XSQ(T)    6.0
 OD T

OBJECT BOUND

 LO DTOC3               0.0

*LO SOLUTION(  10)      224.590381002
*LO SOLUTION(  50)      233.278523083
*LO SOLUTION( 100)      234.286202920
*LO SOLUTION( 500)      235.084407947
*LO SOLUTION(1000)      235.182824435
*LO SOLUTION(5000)      235.154640099

ENDATA


***********************
* SET UP THE FUNCTION *
* AND RANGE ROUTINES  *
***********************

ELEMENTS      DTOC3

INDIVIDUALS

 T  SQ
 F                      YY * YY
 G  YY                  YY + YY
 H  YY        YY        2.0

ENDATA


