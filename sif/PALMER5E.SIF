***************************
* SET UP THE INITIAL DATA *
***************************

NAME          PALMER5E

*   Problem :
*   *********

*   A nonlinear least squares problem
*   arising from chemical kinetics.

*   model: H-N=C=Se TZVP + MP2
*   fitting Y to A0 T_0 + A2 T_2 + A4 T_4 + A6 T_6 + A8 T_8 +
*                A10 T_10 + A12 T_12 + A14 T_14
*                + L * EXP( -K X**2 )
*   where T_i is the i-th (shifted) Chebyshev polynomial

*   Source:
*   M.  Palmer, Edinburgh, private communication.

*   SIF input: Nick Gould, 1992.

*   classification SBR2-RN-8-0

*   Number of data points

 IE M                   23

*   Other data

 IE 1                   1
 IE 2                   2
 IE 12                  12
 IE 14                  14

*   X (radians)

 RE X12                  0.000000 
 RE X13                  1.570796 
 RE X14                  1.396263 
 RE X15                  1.308997 
 RE X16                  1.221730 
 RE X17                  1.125835 
 RE X18                  1.047198 
 RE X19                  0.872665 
 RE X20                  0.698132 
 RE X21                  0.523599 
 RE X22                  0.349066 
 RE X23                  0.174533 

 R= B         X13       
 RM A         B         -1.0D+0
 RM DIFF      B         2.0D+0
 
*   Y( KJmol-1)

 RE Y12                 83.57418
 RE Y13                 81.007654
 RE Y14                 18.983286
 RE Y15                  8.051067
 RE Y16                  2.044762
 RE Y17                  0.000000
 RE Y18                  1.170451
 RE Y19                 10.479881
 RE Y20                 25.785001
 RE Y21                 44.126844
 RE Y22                 62.822177
 RE Y23                 77.719674

VARIABLES

    A0
    A2
    A4
    A6
    A8
    A10
*   A12
*   A14
    K
    L

GROUPS

 DO I         12                       M
 RE T0                  1.0D+0
 AM Y         X(I)      2.0D+0
 R- Y         Y                        A
 R- Y         Y                        B
 R/ Y         Y                        DIFF
 R= T1        Y
 RM 2Y        Y         2.0D+0

 DO J         2                        14
 IA J-1       J         -1
 IA J-2       J         -2
 A* T(J)      2Y                       T(J-1)
 A- T(J)      T(J)                     T(J-2)
 OD J

 ZN O(I)      A0                       T0
 ZN O(I)      A2                       T2
 ZN O(I)      A4                       T4
 ZN O(I)      A6                       T6
 ZN O(I)      A8                       T8
 ZN O(I)      A10                      T10
*ZN O(I)      A12                      T12
*ZN O(I)      A14                      T14
 ND

CONSTANTS

 DO I         12                       M
 Z  PALMER5E  O(I)                     Y(I)
 ND

BOUNDS

 FR PALMER5E  A0
 FR PALMER5E  A2
 FR PALMER5E  A4
 FR PALMER5E  A6
 FR PALMER5E  A8
 FR PALMER5E  A10
*FR PALMER5E  A12
*FR PALMER5E  A14
 FR PALMER5E  L

START POINT

 XV PALMER5E  'DEFAULT' 1.0

 XV PALMER5E  A0         1.9264D+01
 XV PALMER5E  A2        -1.7302D+00
 XV PALMER5E  A4         4.0794D+01
 XV PALMER5E  A6         8.3021D-01
 XV PALMER5E  A8         3.7090D+00
 XV PALMER5E  A10       -1.7723D-01
 XV PALMER5E  K         10.0

ELEMENT TYPE

 EV PROD      K                        L
 EP PROD      XSQR

ELEMENT USES

 DO I         12                       M
 A* XSQR      X(I)                     X(I)

 XT E(I)      PROD
 ZV E(I)      K                        K
 ZV E(I)      L                        L
 ZP E(I)      XSQR                     XSQR

 ND

GROUP TYPE

 GV L2        GVAR

GROUP USES

 DO I         12                       M
 XT O(I)      L2
 XE O(I)      E(I)
 ND

OBJECT BOUND

*   Least square problems are bounded below by zero

 LO PALMER5E                0.0

*   Solution

*LO SOLTN              1.48003482D-04

ENDATA

***********************
* SET UP THE FUNCTION *
* AND RANGE ROUTINES  *
***********************

ELEMENTS      PALMER5E

TEMPORARIES

 R  EXPON
 M  EXP

INDIVIDUALS

 T  PROD
 A  EXPON               EXP( - K * XSQR )
 F                      L * EXPON
 G  K                   - XSQR * L * EXPON
 G  L                   EXPON
 H  K         K         XSQR * XSQR * L * EXPON
 H  K         L         - XSQR * EXPON

ENDATA

*********************
* SET UP THE GROUPS *
* ROUTINE           *
*********************

GROUPS        PALMER5E

INDIVIDUALS

 T  L2
 F                      GVAR * GVAR
 G                      GVAR + GVAR
 H                      2.0
ENDATA









