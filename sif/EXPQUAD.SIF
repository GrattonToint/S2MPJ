***************************
* SET UP THE INITIAL DATA *
***************************

NAME          EXPQUAD

*   Problem :
*   *********

*   A problem with mixed exponential and quadratic terms.

*   SIF input: Ph. Toint, 1992.
*              minor correction by Ph. Shott, Jan 1995.

*   classification OBR2-AN-V-V

*IE N                   12             $-PARAMETER
*IE M                   6              $-PARAMETER

*IE N                   120            $-PARAMETER     original value
*IE M                   10             $-PARAMETER     original value

*IE N                   1200           $-PARAMETER
 IE N                   12             $-PARAMETER     modified for S2X tests
*IE M                   100            $-PARAMETER
 IE M                   6              $-PARAMETER     modified for S2X tests

*   Define constants

 IE 0                   0
 IE 1                   1
 IE 2                   2
 IA M+1       M         1
 IA N-1       N         -1

VARIABLES

 DO I         1                        N
 X  X(I)
 ND

GROUPS

 DO I         1                        N
 RI RI        I
 RM C         RI         - 10.0
 ZN OBJ       X(I)                     C
 ND

CONSTANTS

BOUNDS

 XR EXPQUAD   'DEFAULT'
 DO I         1                        M
 XL EXPQUAD   X(I)       0.0
 XU EXPQUAD   X(I)       10.0
 OD I

START POINT

ELEMENT TYPE

 EV EXP       X                        Y
 EP EXP       P
 EV QUAD      X                        Y

ELEMENT USES

 XT 'DEFAULT' QUAD

 RI RM        M
 DO I         1                        M
 RI RI        I
 R/ C         RI                       RM
 I+ I+1       I                        1
 XT E(I)      EXP
 ZV E(I)      X                        X(I)
 ZV E(I)      Y                        X(I+1)
 ZP E(I)      P                        C
 OD I

 DO I         M+1                      N-1
 ZV E(I)      X                        X(I)
 ZV E(I)      Y                        X(N)
 OD I

GROUP USES
 DO I         1                        N-1
 XE OBJ       E(I)
 OD I

OBJECT BOUND

 LO EXPQUAD             0.0

*   Solution

ENDATA

***********************
* SET UP THE FUNCTION *
* AND RANGE ROUTINES  *
***********************

ELEMENTS      EXPQUAD

TEMPORARIES

 M  EXP
 R  F

INDIVIDUALS

*   Exponential of the product of X and Y divided by 10

 T  EXP

 A  F                   EXP( 0.1 * P * X * Y )
 F                      F
 G  X                   0.1 * P * Y * F
 G  Y                   0.1 * P * X * F
 H  X         X         ( 0.1 * P * Y ) ** 2 * F
 H  Y         Y         ( 0.1 * P * X ) ** 2 * F
 H  Y         X         ( 0.1 + 0.01*P*X*Y ) * F * P

 T  QUAD
 F                      4.0 * X * X + 2.0 * Y * Y + X * Y
 G  X                   8.0 * X + Y
 G  Y                   4.0 * Y + X
 H  X         X         8.0
 H  X         Y         1.0
 H  Y         Y         4.0

ENDATA

