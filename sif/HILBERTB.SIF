***************************
* SET UP THE INITIAL DATA *
***************************

NAME          HILBERTB

*   Problem :
*   *********

*   The perturbed Hilbert quadratic

*   Source: problem 19 (p. 59) in
*   A.R. Buckley,
*   "Test functions for unconstrained minimization",
*   TR 1989CS-3, Mathematics, statistics and computing centre,
*   Dalhousie University, Halifax (CDN), 1989.

*   SIF input: Ph. Toint, Dec 1989.

*   classification QUR2-AN-V-0

*   Dimension of the problem

*IE N                   5              $-PARAMETER     original value
*IE N                   10             $-PARAMETER
 IE N                   10             $-PARAMETER     modified for S2X tests
*IE N                   50             $-PARAMETER

*   Conditioning parameter

 RE D                   5.0            $-PARAMETER

*   Other parameters

 IE 1                   1

VARIABLES

 DO I         1                        N
 X  X(I)
 ND

GROUPS

 DO I         1                        N
 DO J         1                        I
 XN G(I,J)
 ND

BOUNDS

 FR HILBERTB  'DEFAULT'

START POINT

 XV HILBERTB  'DEFAULT' -3.0

ELEMENT TYPE

 EV 2PR       X                        Y
 EV SQ        X

ELEMENT USES

 DO I         1                        N
 IA I-1       I         -1

 DO J         1                        I-1
 XT E(I,J)    2PR
 ZV E(I,J)    X                        X(I)
 ZV E(I,J)    Y                        X(J)
 OD J

 XT E(I,I)    SQ
 ZV E(I,I)    X                        X(I)
 ND

GROUP USES

 DO I         1                        N
 IA I-1       I         -1

 DO J         1                        I-1
 I+ I+J       I                        J
 IA I+J-1     I+J       -1
 RI RINVH     I+J-1
 RD HIJ       RINVH     1.0
 ZE G(I,J)    E(I,J)                   HIJ
 OD J

 IM 2I        I         2
 IA 2I-1      2I        -1
 RI RH0       2I-1
 RD HII       RH0       1.0
 RM HII/2     HII       0.5
 R+ COEFF     HII/2                    D
 ZE G(I,I)    E(I,I)                   COEFF
 ND

OBJECT BOUND

*   Solution

*LO SOLTN               0.0

ENDATA

***********************
* SET UP THE FUNCTION *
* AND RANGE ROUTINES  *
***********************

ELEMENTS      HILBERTB

INDIVIDUALS

 T  2PR
 F                      X * Y
 G  X                   Y
 G  Y                   X
 H  X         Y         1.0


 T  SQ
 F                      X * X
 G  X                   X + X
 H  X         X         2.0

ENDATA
