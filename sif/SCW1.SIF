***************************
* SET UP THE INITIAL DATA *
***************************

NAME          SCW1

*   Problem :
*   *********

*   Source: a discretization of an infinite-demsional problem proposed 
*   by Simon Chandler-Wilde (U. Reading):

*   Given a function u in C[0,2 pi] with ||u||_infty <= 1, find the 
*   supremum of c^2(u) + s^2(u), where
*     c(u) = int_0^2 pi cos(t)u(t) dt and
*     s(u) = int_0^2 pi sin(t)u(t) dt      

*   The discretized version ignores the required continuity, and 
*   posits a piecewise constant solution that oscilates between
*   plus and minus one. The anticipated solution is -16.

*   SIF input: Nick Gould, July 2020

*   classification SLR2-MN-V-V

*   Number of internal knots

*IE K                   1              $-PARAMETER
 IE K                   7              $-PARAMETER     modified for S2X tests
*IE K                   3              $-PARAMETER
*IE K                   7              $-PARAMETER     original value
*IE K                   10             $-PARAMETER
*IE K                   100            $-PARAMETER

*   Other useful parameters

 IE 0                   0
 IE 1                   1
 IE 2                   2
 IA K+1       K         1
 RI RK+1      K+1
 RF PI/4      ARCTAN    1.0
 RM PI        PI/4      4.0
 RM 2PI       PI        2.0
 R/ 2PI/K+1   2PI                      RK+1

VARIABLES

 DO I         0                        K+1
 X  T(I)
 ND

GROUPS

 N  S
 N  C
 DO I         0                        K
 IA I+1       I         1
 XG CON(I)    T(I+1)    1.0            T(I)      -1.0
 ND

BOUNDS

 XX SCW       T(0)      0.0
 DO I         1                        K
 XL SCW       T(I)      0.0
 ZU SCW       T(I)                     2PI
 ND
 ZX SCW       T(K+1)                   2PI

START POINT

 X  SCW       T(1)      1.0
 DO I         2                        K
 RI RI        I
 R* START     RI                       2PI/K+1
*Z  SCW       T(I)                     START
 X  SCW       T(I)      0.0
 ND

ELEMENT TYPE

 EV SINT      T
 EV COST      T

ELEMENT USES

 DO I         0                        K+1
 XT S(I)      SINT
 ZV S(I)      T                        T(I)
 XT C(I)      COST
 ZV C(I)      T                        T(I)
 ND

GROUP TYPE

 GV MAXSQ     ALPHA

GROUP USES

 XT C         MAXSQ
 DO I         0                        K
 DI I         2
 IA I+1       I         1
 XE C         S(I+1)    1.0            S(I)      -1.0
 ND
 DO I         1                        K
 DI I         2
 IA I+1       I         1
 XE C         S(I+1)    -1.0           S(I)      1.0
 ND

 XT S         MAXSQ
 DO I         0                        K
 DI I         2
 IA I+1       I         1
 XE S         C(I+1)    -1.0           C(I)      1.0
 ND
 DO I         1                        K
 DI I         2
 IA I+1       I         1
 XE S         C(I+1)    1.0            C(I)      -1.0
 ND

OBJECT BOUND

*LO SCW                 0.0

*   Solution

ENDATA

***********************
* SET UP THE FUNCTION *
* AND RANGE ROUTINES  *
***********************

ELEMENTS      SCW1

TEMPORARIES

 R  S
 R  C
 M  SIN
 M  COS

INDIVIDUALS

 T  SINT
 A  S                   SIN( T )
 F                      S
 G  T                   COS( T )
 H  T         T         -S

 T  COST
 A  C                   COS( T )
 F                      C
 G  T                   - SIN( T )
 H  T         T         - C

ENDATA

***********************
* SET UP THE FUNCTION *
* AND RANGE ROUTINES  *
***********************

GROUPS        SCW1

INDIVIDUALS

 T  MAXSQ
 F                      - ALPHA * ALPHA
 G                      - ALPHA - ALPHA
 H                      - 2.0D+0

ENDATA
