***************************
* SET UP THE INITIAL DATA *
***************************

NAME          ORTHRGDS

*   Problem :
*   *********

*   An orthogonal regression problem.

*   The problem is to fit (orthogonally) a circle to a set of points
*   in the plane. This set of points is generated by perturbing a
*   first set lying exactly on a predefined circle centered at the
*   origin. Each point mapped to the center of the circle has a constraint 
*   gradient of zero, making the Jacobian of the constraints singular.
*
*   Certain data points (which serve to define the initial estimate
*   of the unknowns) are modified so that they will converge to
*   center of the circle, allowing us to study ill-conditioned
*   Jacobians.
*   With 10 singular points, the final objective is SOLTN = 118.037

*   Source: adapted from:
*   M. Gulliksson,
*   "Algorithms for nonlinear Least-squares with Applications to
*   Orthogonal Regression",
*   UMINF-178.90, University of Umea, Sweden, 1990.

*   SIF input: Ph. Toint, Mar 1991 and T. Plantenga, May 1993.

*   classification QOR2-AY-V-V

*   Number of data points
*   (number of variables = 2 NPTS + 3 )

*IE NPTS                10             $-PARAMETER n = 23
*IE NPTS                50             $-PARAMETER n = 103
*IE NPTS                76             $-PARAMETER n = 155    original value
*IE NPTS                100            $-PARAMETER n = 203
*IE NPTS                250            $-PARAMETER n = 503
*IE NPTS                500            $-PARAMETER n = 1003
*IE NPTS                2500           $-PARAMETER n = 5003
 IE NPTS                20             $-PARAMETER     modified for S2X tests
*IE NPTS                5000           $-PARAMETER n = 10003

*   True circle parameters (centered at the origin)

 RE TZ3                 1.7

*   Perturbation parameters

 RE PSEED               237.1531
 RE PSIZE               0.2

*   Constants

 IE 1                   1
 IE 0                   0
 IE TDP_lo              5
 IE TDP_hi              14

 RE PI                  3.1415926535

*   Computed parameters

 RM 2PI       PI        2.0
 RI RNPTS     NPTS
 RD ICR0      RNPTS     1.0
 R* INCR      ICR0                     2PI
 R* Z3SQ      TZ3                      TZ3
 RA 1+TZ3SQ   Z3SQ      1.0

*   Construct the data points

 DO I         1                        NPTS
 IA I-1       I         -1
 RI RI-1      I-1
 R* THETA     RI-1                     INCR
 R( ST        SIN                      THETA
 R( CT        COS                      THETA
 R+ FACT      1+TZ3SQ                  CT
 R* R1        FACT                     CT
 R* R2        FACT                     ST
 R* XSEED     THETA                    PSEED
 R( SSEED     COS                      XSEED
 R* PER-1     PSIZE                    SSEED
 RA PERT      PER-1      1.0
 A* XD(I)     R1                       PERT
 A* YD(I)     R2                       PERT
 ND

VARIABLES

*   Parameters of the circle

    Z1
    Z2
    Z3

 DO I         1                        NPTS

*   Projections of the data points onto the circle

 X  X(I)
 X  Y(I)

 ND

GROUPS

 DO I         1                        NPTS
 XN OX(I)     X(I)       1.0
 XN OY(I)     Y(I)       1.0
 XE E(I)
 ND

CONSTANTS

 DO I         1                        NPTS
 ZN ORTHRGDS  OX(I)                    XD(I)
 ZN ORTHRGDS  OY(I)                    YD(I)
 ND

BOUNDS

 FR ORTHRGDS  'DEFAULT'

START POINT

    ORTHRGDS  Z1        1.0
    ORTHRGDS  Z2        0.0
    ORTHRGDS  Z3        1.0

 DO I         1                        NPTS
 Z  ORTHRGDS  X(I)                     XD(I)
 Z  ORTHRGDS  Y(I)                     YD(I)
 ND
 DO I         TDP_lo                   TDP_hi
 X  ORTHRGDS  X(I)      1.8
 X  ORTHRGDS  Y(I)      1.0
 ND

ELEMENT TYPE

 EV TA        X                        Y
 EV TA        ZA                       ZB
 IV TA        DX                       DY

 EV TB        X                        Y
 EV TB        ZA                       ZB
 EV TB        ZC
 IV TB        DX                       DY
 IV TB        ZZ

ELEMENT USES

 DO I         1                        NPTS

 XT EA(I)     TA
 ZV EA(I)     X                        X(I)
 ZV EA(I)     Y                        Y(I)
 ZV EA(I)     ZA                       Z1
 ZV EA(I)     ZB                       Z2

 XT EB(I)     TB
 ZV EB(I)     X                        X(I)
 ZV EB(I)     Y                        Y(I)
 ZV EB(I)     ZA                       Z1
 ZV EB(I)     ZB                       Z2
 ZV EB(I)     ZC                       Z3

 ND

GROUP TYPE

 GV L2        GVAR

GROUP USES

 DO I         1                        NPTS
 XT OX(I)     L2
 XT OY(I)     L2
 XE E(I)      EA(I)                    EB(I)     -1.0
 ND

OBJECT BOUND

 LO ORTHRGDS            0.0

*   Solution

*LO SOLTN(10)           3.412121065
*LO SOLTN(50)           15.59042181
*LO SOLTN(250)          76.10435792
*LO SOLTN(500)          151.2351183
*LO SOLTN(2500)         ???
*LO SOLTN(5000)         ???

ENDATA

***********************
* SET UP THE FUNCTION *
* AND RANGE ROUTINES  *
***********************

ELEMENTS      ORTHRGDS

TEMPORARIES

 R  T
 R  T1
 R  T1SQ
 R  ZZSQ

INDIVIDUALS

 T  TA
 R  DX        X         1.0            ZA        -1.0
 R  DY        Y         1.0            ZB        -1.0
 A  T                   DX * DX + DY * DY
 F                      T * T
 G  DX                  4.0 * T * DX
 G  DY                  4.0 * T * DY
 H  DX        DX        4.0 * ( T + 2.0 * DX * DX )
 H  DX        DY        8.0 * DX * DY
 H  DY        DY        4.0 * ( T + 2.0 * DY * DY )

 T  TB
 R  DX        X         1.0            ZA        -1.0
 R  DY        Y         1.0            ZB        -1.0
 R  ZZ        ZC        1.0
 A  T                   DX * DX + DY * DY
 A  ZZSQ                ZZ * ZZ
 A  T1                  1.0 + ZZSQ
 A  T1SQ                T1 * T1
 F                      T * T1SQ
 G  DX                  2.0 * DX * T1SQ
 G  DY                  2.0 * DY * T1SQ
 G  ZZ                  4.0 * T * T1 * ZZ
 H  DX        DX        2.0 * T1SQ
 H  DX        ZZ        8.0 * DX * T1 * ZZ
 H  DY        DY        2.0 * T1SQ
 H  DY        ZZ        8.0 * DY * T1 * ZZ
 H  ZZ        ZZ        4.0 * T * ( 2.0 * ZZSQ + T1)

ENDATA

*********************
* SET UP THE GROUPS *
* ROUTINE           *
*********************

GROUPS        ORTHRGDS

*   Least-square groups

INDIVIDUALS

 T  L2
 F                      GVAR * GVAR
 G                      GVAR + GVAR
 H                      2.0

ENDATA
