***************************
* SET UP THE INITIAL DATA *
***************************

NAME          COOLHANS

*   Problem :
*   *********

*   A problem arising from the analysis of a Cooley-Hansen economy with
*   loglinear approximation.  The problem is to solve the matrix equation
*
*                 A * X * X + B * X + C = 0
*
*   where A, B and C are known N times N matrices and X an unknown matrix
*   of matching dimension.  The instance considered here has N = 3.

*   Source:
*   S. Ceria, private communication, 1995.

*   SIF input: Ph. Toint, Feb 1995.

*   classification NQR2-RN-9-9

*   order of the matrix equation

 IE N                   3

*   matrix data

 RE A1,1                0.0
 RE A2,1                0.13725D-6
 RE A3,1                0.0
 RE A1,2                0.0
 RE A2,2                937.62
 RE A3,2                0.0
 RE A1,3                0.0
 RE A2,3                -42.207
 RE A3,3                0.0

 RE B1,1                0.0060893
 RE B2,1                0.13880D-6
 RE B3,1                -0.13877d-6
 RE B1,2                -44.292
 RE B2,2                -1886.0
 RE B3,2                42.362
 RE B1,3                2.0011
 RE B2,3                42.362
 RE B3,3                -2.0705

 RE C1,1                0.0
 RE C2,1                0.0
 RE C3,1                0.0
 RE C1,2                44.792
 RE C2,2                948.21
 RE C3,2                -42.684
 RE C1,3                0.0
 RE C2,3                0.0
 RE C3,3                0.0

*   useful constants

 IE 1                   1
 IE 2                   2
 IE 3                   3

VARIABLES

 DO I         1                        N
 DO J         1                        N
 X  X(I,J)
 ND

GROUPS

 DO K         1                        N
 DO L         1                        N
 DO M         1                        N
 ZE G(K,L)    X(M,L)                   B(K,M)
 ND

CONSTANTS

 DO K         1                        N
 DO L         1                        N
 AM -C        C(K,L)    -1.0
 Z  COOLHANS  G(K,L)                   -C
 ND

BOUNDS

 FR COOLHANS  'DEFAULT'

* DO K         1                        N
* XX COOLHANS  X(1,K)    0.0
* XX COOLHANS  X(3,K)    0.0
* OD K

ELEMENT TYPE

 EV 2PR       XX                       YY

ELEMENT USES

 DO K         1                        N
 DO L         1                        N
 DO M         1                        N
 XT E(K,M,L)  2PR
 ZV E(K,M,L)  XX                       X(K,M)
 ZV E(K,M,L)  YY                       X(M,L)
 ND

GROUP USES

 DO L         1                        N
 DO P         1                        N
 DO M         1                        N
 ZE G(1,L)    E(P,M,L)                 A(1,P)
 ND

 DO L         1                        N
 DO P         1                        N
 DO M         1                        N
 ZE G(2,L)    E(P,M,L)                 A(2,P)
 ND

 DO L         1                        N
 DO P         1                        N
 DO M         1                        N
 ZE G(3,L)    E(P,M,L)                 A(3,P)
 ND

OBJECT BOUND

 LO COOLHANS             0.0

*   Solution

*LO SOLTN                0.0

ENDATA

***********************
* SET UP THE FUNCTION *
* AND RANGE ROUTINES  *
***********************

ELEMENTS      COOLHANS

INDIVIDUALS

 T  2PR
 F                      XX * YY
 G  XX                  YY
 G  YY                  XX
 H  XX        YY        1.0

ENDATA
