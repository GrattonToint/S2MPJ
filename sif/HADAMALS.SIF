***************************
* SET UP THE INITIAL DATA *
***************************

NAME          HADAMALS

*   Problem :
*   --------

*   An attempt to find Hadamard matrices of order N.

*   The problem is to find an N by N orthonormal matrix Q,
*   with column norms N, whose entries are plus or minus one.

*   Source:  A suggestion by Alan Edelman (MIT).

*   SIF input: Nick Gould, Nov 1993.

*   classification OBR2-RN-V-V

*   The dimension of the matrix (=> N**2 variables).

*IE N                   2              $-PARAMETER    original value
*IE N                   4              $-PARAMETER
*IE N                   6              $-PARAMETER
*IE N                   8              $-PARAMETER
*IE N                   10             $-PARAMETER
*IE N                   12             $-PARAMETER
*IE N                   14             $-PARAMETER
*IE N                   16             $-PARAMETER
*IE N                   18             $-PARAMETER
*IE N                   20             $-PARAMETER
 IE N                   10             $-PARAMETER     modified for S2X tests
*IE N                   32             $-PARAMETER
*IE N                   64             $-PARAMETER
*IE N                   128            $-PARAMETER
*IE N                   256            $-PARAMETER
*IE N                   428            $-PARAMETER

*   other parameter definitions

 IE 1                   1
 IE 2                   2
 RI RN        N
 I/ N/2       N                        2
 IA N/2+1     N/2       1
   
VARIABLES

*  Define the largest entry in absolute value.

 DO J         1                        N
 DO I         1                        N

*  Define the orthogonal matrix

 X  Q(I,J)

 ND

GROUPS

*  Introduce the orthogonality-equations Q(T) Q - sqrt(N) I = 0.

 DO J         1                        N
 DO I         1                        J
 XN O(I,J)
 ND

*  Introduce the restrictions that each entry is + 1 or - 1.

 DO J         1                        N
 DO I         2                        N
 XN S(I,J)    
 ND

CONSTANTS

 DO J         1                        N
 Z  HADAMALS  O(J,J)                   RN
 ND

 DO J         1                        N
 DO I         2                        N
 X  HADAMALS  S(I,J)    1.0
 ND

BOUNDS

 LO HADAMALS  'DEFAULT' -1.0
 UP HADAMALS  'DEFAULT' 1.0

 DO I         1                        N/2
 XX HADAMALS  Q(I,1)    1.0   
 ND

 DO I         N/2+1                    N
 XX HADAMALS  Q(I,1)    -1.0   
 ND

START POINT

 DO J         1                        N
 DO I         1                        N/2
 XV HADAMALS  Q(I,J)    0.9
 OD I

 DO I         N/2+1                    N
 XV HADAMALS  Q(I,J)    -0.9
 OD I
 ND

ELEMENT TYPE

 EV SQR       Q1
 EV 2PROD     Q1                       Q2

ELEMENT USES

 DO J         1                        N
 DO I         1                        J
 DO K         1                        N

 XT O(I,J,K)  2PROD
 ZV O(I,J,K)  Q1                       Q(K,I)
 ZV O(I,J,K)  Q2                       Q(K,J)

 ND

 DO J         1                        N
 DO I         2                        N

 XT S(I,J)    SQR
 ZV S(I,J)    Q1                       Q(I,J)

 ND

GROUP TYPE

 GV L2        GVAR
 GV LARGEL2   GVAR

GROUP USES


 DO J         1                        N
 DO I         1                        J
 XT O(I,J)    L2
 DO K         1                        N
 XE O(I,J)    O(I,J,K)
 ND

 DO J         1                        N
 DO I         2                        N
 XT S(I,J)    LARGEL2
 XE S(I,J)    S(I,J)
 ND

ENDATA

***********************
* SET UP THE FUNCTION *
* AND RANGE ROUTINES  *
***********************

ELEMENTS      HADAMALS

INDIVIDUALS

 T  SQR
 F                      Q1 * Q1
 G  Q1                  Q1 + Q1
 H  Q1        Q1        2.0D+0

 T  2PROD
 F                      Q1 * Q2
 G  Q1                       Q2
 G  Q2                  Q1
 H  Q1        Q2        1.0D+0

ENDATA

*********************
* SET UP THE GROUPS *
* ROUTINE           *
*********************

GROUPS        HADAMALS

TEMPORARIES
 
 R  FACTOR

GLOBALS

 A  FACTOR              1.0D+0

INDIVIDUALS

 T  L2
 F                      GVAR * GVAR
 G                      GVAR + GVAR
 H                      2.0D+0

 T  LARGEL2
 F                      FACTOR * GVAR * GVAR
 G                      2.0D+0 * FACTOR * GVAR
 H                      2.0D+0 * FACTOR

ENDATA


