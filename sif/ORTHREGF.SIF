***************************
* SET UP THE INITIAL DATA *
***************************

NAME          ORTHREGF

*   Problem :
*   *********

*   An orthogonal regression problem

*   The problem is to fit (orthogonally) an torus to a
*   set of points in 3D space. This set of points is generated by
*   perturbing a first set lying exactly on a predefined torus
*   centered at the origin.

*   Source:
*   M. Gulliksson,
*   "Algorithms for nonlinear Least-squares with Applications to
*   Orthogonal Regression",
*   UMINF-178.90, University of Umea, Sweden, 1990.

*   SIF input: Ph. Toint, June 1990.
*              minor correction by Ph. Shott, Jan 1995.

*   classification QOR2-AY-V-V

*   square root of the number of data points
*   (number of variables = 3 * NPTS**2 + 5 )

*IE NPTS                5              $-PARAMETER n = 80    original value
*IE NPTS                7              $-PARAMETER n = 152
*IE NPTS                10             $-PARAMETER n = 305
*IE NPTS                15             $-PARAMETER n = 680
*IE NPTS                20             $-PARAMETER n = 1205
*IE NPTS                40             $-PARAMETER n = 4805
 IE NPTS                5              $-PARAMETER     modified for S2X tests

*   True torus parameters (centered at the origin)

 RE TP4                 1.7
 RE TP5                 0.8

*   Perturbation parameters

 RE PSEED               237.1531
 RE PSIZE               0.2

*   Constants

 IE 1                   1
 IE 5                   5

 RE PI                  3.1415926535

*   Computed parameters

 RM 2PI       PI        2.0
 RI RNPTS     NPTS
 RD ICR0      RNPTS     1.0
 R* INCR      ICR0                     2PI

*   Construct the data points

 DO I         1                        NPTS
 IA I-1       I         -1
 RI RI-1      I-1
 R* THETA1    RI-1                     INCR
 R( ST1       SIN                      THETA1
 R( CT1       COS                      THETA1
 R* P5CT1     TP5                      CT1
 R+ P4P5CT1   TP4                      P5CT1
 R* R3        TP5                      ST1

 DO J         1                        NPTS
 IA J-1       J         -1
 RI RJ-1      J-1
 R* THETA2    RJ-1                     INCR
 R( ST2       SIN                      THETA2
 R( CT2       COS                      THETA2

 R* R1        P4P5CT1                  CT2
 R* R2        P4P5CT1                  ST2
 R* XSEED     THETA2                   PSEED
 R( SSEED     COS                      XSEED
 R* PER-1     PSIZE                    SSEED
 RA PERT      PER-1      1.0
 A* XD(I,J)   R1                       PERT
 A* YD(I,J)   R2                       PERT
 A* ZD(I,J)   R3                       PERT
 ND

VARIABLES

*   Parameters of the torus

 DO I         1                        5
 X  P(I)
 ND

*   Projections of the data points onto the torus

 DO I         1                        NPTS
 DO J         1                        NPTS
 X  X(I,J)
 X  Y(I,J)
 X  Z(I,J)
 ND

GROUPS

 N  OBJ

 DO I         1                        NPTS
 DO J         1                        NPTS
 XN OX(I,J)   X(I,J)    1.0
 XN OY(I,J)   Y(I,J)    1.0
 XN OZ(I,J)   Z(I,J)    1.0
 XE A(I,J)
 ND

CONSTANTS

 DO I         1                        NPTS
 DO J         1                        NPTS
 Z  ORTHREGF  OX(I,J)                  XD(I,J)
 Z  ORTHREGF  OY(I,J)                  YD(I,J)
 Z  ORTHREGF  OZ(I,J)                  ZD(I,J)
 ND

BOUNDS

 FR ORTHREGF  'DEFAULT'

 XL ORTHREGF  P4        0.001
 XL ORTHREGF  P5        0.001

START POINT

    ORTHREGF  P1        1.0
    ORTHREGF  P2        0.0
    ORTHREGF  P3        1.0
    ORTHREGF  P4        1.0
    ORTHREGF  P5        0.5

 DO I         1                        NPTS
 DO J         1                        NPTS
 Z  ORTHREGF  X(I,J)                   XD(I,J)
 Z  ORTHREGF  Y(I,J)                   YD(I,J)
 Z  ORTHREGF  Z(I,J)                   ZD(I,J)
 ND

ELEMENT TYPE

 EV TA        XX                       YY
 EV TA        A                        B
 EV TA        C
 IV TA        XMA                      YMB
 IV TA        CC

 EV ISQ       Z                        P
 IV ISQ       ZMP

 EV SQ        XX

ELEMENT USES

 DO I         1                        NPTS
 DO J         1                        NPTS

 XT EA(I,J)   TA
 ZV EA(I,J)   XX                       X(I,J)
 ZV EA(I,J)   YY                       Y(I,J)
 ZV EA(I,J)   A                        P1
 ZV EA(I,J)   B                        P2
 ZV EA(I,J)   C                        P4

 XT EB(I,J)   ISQ
 ZV EB(I,J)   Z                        Z(I,J)
 ZV EB(I,J)   P                        P3

 XT EC(I,J)   SQ
 ZV EC(I,J)   XX                       P5

 ND

GROUP TYPE

 GV L2        GVAR

GROUP USES

 DO I         1                        NPTS
 DO J         1                        NPTS
 XT OX(I,J)   L2
 XT OY(I,J)   L2
 XT OZ(I,J)   L2
 XE A(I,J)    EA(I,J)                  EB(I,J)
 XE A(I,J)    EC(I,J)   -1.0
 ND

OBJECT BOUND

 LO ORTHREGF            0.0

*   Solution

*LO SOLTN(5)            0.990089426
*LO SOLTN(7)            1.315031322
*LO SOLTN(10)           4.515848902
*LO SOLTN(15)           9.185538338
*LO SOLTN(20)           16.20054380

ENDATA

***********************
* SET UP THE FUNCTION *
* AND RANGE ROUTINES  *
***********************

ELEMENTS      ORTHREGF

TEMPORARIES

 R  CCSQ
 R  CCCB
 R  XXYY
 R  T
 R  DTDX
 R  DTDY
 R  DTDC
 R  D2TDX2
 R  D2TDY2
 R  D2TDC2
 R  D2TDXC
 R  D2TDYC
 R  S
 R  DSDX
 R  DSDY
 R  DSDC
 R  D2SDX2
 R  D2SDY2
 R  D2SDC2
 R  D2SDXY
 R  D2SDXC
 R  D2SDYC
 R  R
 R  SS
 R  SPS
 M  SQRT

INDIVIDUALS

 T  TA
 R  XMA       XX        1.0            A         -1.0
 R  YMB       YY        1.0            B         -1.0
 R  CC        C         1.0
 A  CCSQ                CC * CC
 A  CCCB                CCSQ * CC
 A  XXYY                XMA * XMA + YMB * YMB
 A  T                   XXYY / CCSQ
 A  DTDX                2.0 * XMA / CCSQ
 A  DTDY                2.0 * YMB / CCSQ
 A  DTDC                - 2.0 * XXYY / CCCB
 A  D2TDX2              2.0 / CCSQ
 A  D2TDY2              2.0 / CCSQ
 A  D2TDC2              6.0 * XXYY / ( CCSQ * CCSQ)
 A  D2TDXC              - 4.0 * XMA / CCCB
 A  D2TDYC              - 4.0 * YMB / CCCB
 A  S                   SQRT( T )
 A  R                   0.5 / S
 A  DSDX                R * DTDX
 A  DSDY                R * DTDY
 A  DSDC                R * DTDC
 A  D2SDX2              R * ( D2TDX2 - 0.5 * DTDX * DTDX / T )
 A  D2SDY2              R * ( D2TDY2 - 0.5 * DTDY * DTDY / T )
 A  D2SDC2              R * ( D2TDC2 - 0.5 * DTDC * DTDC / T )
 A  D2SDXY              -0.5 * DTDX * DSDY / T
 A  D2SDXC              R * ( D2TDXC - 0.5 * DTDX * DTDC / T )
 A  D2SDYC              R * ( D2TDYC - 0.5 * DTDY * DTDC / T )
 A  SS                  S - 1.0
 A  SPS                 SS + SS
 F                      SS * SS
 G  XMA                 SPS * DSDX
 G  YMB                 SPS * DSDY
 G  CC                  SPS * DSDC
 H  XMA       XMA       SPS * D2SDX2 + 2.0 * DSDX * DSDX
 H  XMA       YMB       SPS * D2SDXY + 2.0 * DSDX * DSDY
 H  XMA       CC        SPS * D2SDXC + 2.0 * DSDX * DSDC
 H  YMB       YMB       SPS * D2SDY2 + 2.0 * DSDY * DSDY
 H  YMB       CC        SPS * D2SDYC + 2.0 * DSDY * DSDC
 H  CC        CC        SPS * D2SDC2 + 2.0 * DSDC * DSDC

 T  ISQ
 R  ZMP       Z         1.0            P         -1.0
 F                      ZMP * ZMP
 G  ZMP                 ZMP + ZMP
 H  ZMP       ZMP       2.0

 T  SQ
 F                      XX * XX
 G  XX                  XX + XX
 H  XX        XX        2.0

ENDATA

*********************
* SET UP THE GROUPS *
* ROUTINE           *
*********************

GROUPS        ORTHREGF

*   Least-square groups

INDIVIDUALS

 T  L2
 F                      GVAR * GVAR
 G                      GVAR + GVAR
 H                      2.0

ENDATA
