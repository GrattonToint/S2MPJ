***************************
* SET UP THE INITIAL DATA *
***************************

NAME          LUKVLI2 

*   Problem :
*   *********

*   Source: Problem 5.2, the chained Wood function with 
*   Broyden banded constraints, due to L. Luksan and J. Vlcek,
*   "Sparse and partially separable test problems for 
*   unconstrained and equality constrained optimization",
*   Technical Report 767, Inst. Computer Science, Academy of Sciences
*   of the Czech Republic, 182 07 Prague, Czech Republic, 1999

*   Equality constraints changed to inequalities

*   SIF input: Nick Gould, April 2001

*   classification OOR2-AY-V-V

*   some useful parameters, including N, the number of variables.

*IE N                   100            $-PARAMETER
*IE N                   1000           $-PARAMETER
*IE N                   10000          $-PARAMETER
 IE N                   10             $-PARAMETER     modified for S2X tests
*IE N                   100000         $-PARAMETER

*  other useful parameters

 IE 0                   0
 IE 1                   1
 IE 2                   2
 IE 6                   6
 IE 7                   7

 I/ N/2       N                        2
 IA N/2-1     N/2       -1
 IA N-1       N         -1
 IA N-2       N         -2

 RE 1.0                 1.0
 RE 90.0                90.0
 R/ 1/90      1.0                      90.0

VARIABLES

 DO I         1                        N
 X  X(I)
 ND

GROUPS

 DO I         1                        N/2-1
 IM 2I        I         2
 IA 2I-1      2I        -1
 IA 2I+1      2I        1
 IA 2I+2      2I        2
 XN A(I)      X(2I)     -1.0
 XN A(I)      'SCALE'   0.01
 XN B(I)      X(2I-1)   1.0
 XN C(I)      X(2I+2)   -1.0
 ZN C(I)      'SCALE'                  1/90
 XN D(I)      X(2I+1)   -1.0
 XN E(I)      X(2I)     1.0            X(2I+2)   1.0
 XN E(I)      'SCALE'   0.1
 XN F(I)      X(2I)     1.0            X(2I-1)   -1.0
 XN F(I)      'SCALE'   10.0
 ND

 DO K         6                        N-2
 IA K+1       K         1
 IA K-5       K         -5
 XL C(K)      X(K)      2.0
 DO I         K-5                      K+1
 XL C(K)      X(I)      1.0
 ND

CONSTANTS

 DO I         1                        N/2-1
 X  RHS       B(I)      1.0
 X  RHS       D(I)      1.0
 X  RHS       E(I)      2.0
 ND

 DO K         6                        N-2
 X  RHS       C(K)      -1.0
 ND

BOUNDS

 FR BND       'DEFAULT'

START POINT

 DO I         1                        N
 DI I         2
 XV LUKVLI1   X(I)      -2.0
 ND

 DO I         2                        N
 DI I         2
 XV LUKVLI1   X(I)      1.0
 ND

ELEMENT TYPE

 EV SQR       V
 EV CUBE      V

ELEMENT USES

 DO I         1                        N/2-1
 IM 2I        I         2
 IA 2I-1      2I        -1
 IA 2I+1      2I        1
 XT A(I)      SQR
 ZV A(I)      V                        X(2I-1)
 XT C(I)      SQR
 ZV C(I)      V                        X(2I+1)
 ND

 DO K         6                        N-2
 XT U(K)      CUBE
 ZV U(K)      V                        X(K)
 ND

 DO K         1                        N-1
 XT S(K)      SQR
 ZV S(K)      V                        X(K)
 ND

GROUP TYPE

 GV L2        GVAR

GROUP USES

 DO I         1                        N/2-1
 XT A(I)      L2
 XE A(I)      A(I)
 XT B(I)      L2
 XT C(I)      L2
 XE C(I)      C(I)
 XT D(I)      L2
 XT E(I)      L2
 XT F(I)      L2
 ND

 DO K         6                        N-2
 IA K+1       K         1
 IA K-5       K         -5
 XE C(K)      U(K)      5.0
 DO I         K-5                      K+1
 XE C(K)      S(I)      1.0
 ND

OBJECT BOUND

 LO LUKVLI2             0.0

*   Solution

*LO SOLTN               2.39390E+04

ENDATA

***********************
* SET UP THE FUNCTION *
* AND RANGE ROUTINES  *
***********************

ELEMENTS      LUKVLI2

INDIVIDUALS

 T  SQR
 F                      V * V
 G  V                   2.0 * V
 H  V         V         2.0

 T  CUBE
 F                      V ** 3
 G  V                   3.0 * V ** 2
 H  V         V         6.0 * V

ENDATA

*********************
* SET UP THE GROUPS *
* ROUTINE           *
*********************

GROUPS        LUKVLI2 

INDIVIDUALS

 T  L2
 F                      GVAR * GVAR
 G                      GVAR + GVAR
 H                      2.0

ENDATA
