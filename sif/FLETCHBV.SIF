
***************************
* SET UP THE INITIAL DATA *
***************************

NAME          FLETCHBV

*   Problem :
*   *********

*   Another Boundary Value problem.

*   Source:  The first problem given by
*   R. Fletcher,
*   "An optimal positive definite update for sparse Hessian matrices"
*   Numerical Analysis report NA/145, University of Dundee, 1992.

*   N.B. This formulation is incorrect. See FLETCBV2.SIF for
*        the correct version.

*   SDIF input: Nick Gould, Oct 1992.

*   classification OUR2-AN-V-0

*   The number of variables is N.

*IE N                   10             $-PARAMETER     original value
*IE N                   100            $-PARAMETER
*IE N                   1000           $-PARAMETER
*IE N                   5000           $-PARAMETER
 IE N                   10             $-PARAMETER     modified for S2X tests
*IE N                   10000          $-PARAMETER

*  KAPPA a parameter.

 RE KAPPA               1.0            $-PARAMETER
*RE KAPPA               0.0            $-PARAMETER

 RE OBJSCALE            1.0D+0

*   Define useful parameters

 IE 0                   0
 IE 1                   1
 IE 2                   2
 RE 1.0                 1.0
 IA N-1       N         -1

 IA N+1       N         1
 RI RN+1      N+1
 R/ H         1.0                      RN+1
 R* H2        H                        H
 R* 1/H2      RN+1                     RN+1
 R* KAPPA/H2  1/H2                     KAPPA
 RM -KAPPA/H2 KAPPA/H2  -1.0
 RM -2/H2     1/H2      -2.0
 RM -1-2/H2   -2/H2     -1.0

VARIABLES

 DO I         1                        N
 X  X(I)
 ND

GROUPS

 ZN G(0)      'SCALE'                  OBJSCALE
 XN G(0)      X(1)      1.0

 DO I         1                        N-1
 IA I+1       I         1
 ZN G(I)      'SCALE'                  OBJSCALE
 XN G(I)      X(I)      1.0            X(I+1)    -1.0
 ND

 ZN G(N)      'SCALE'                  OBJSCALE
 XN G(N)      X(N)       1.0

 DO I         1                        N-1
 ZN L(I)      'SCALE'                  OBJSCALE
 ZN L(I)      X(I)                     -2/H2
 ND

 ZN L(N)      'SCALE'                  OBJSCALE
 ZN L(N)      X(N)                     -1-2/H2

 DO I         1                        N
 ZN C(I)      'SCALE'                  OBJSCALE
 ZN C(I)
 ND

BOUNDS

 FR FLETCHBV  'DEFAULT'

START POINT

 DO I         1                        N
 RI RI        I
 R* IH        RI                       H
 Z  FLETCHBV  X(I)                     IH
 ND

ELEMENT TYPE

 EV COS       V

ELEMENT USES

 T  'DEFAULT' COS

 DO I         1                        N
 ZV C(I)      V                        X(I)
 ND

GROUP TYPE

 GV HALFL2    GVAR

GROUP USES

 DO I         0                        N
 XT G(I)      HALFL2
 ND

 DO I         1                        N
 ZE C(I)      C(I)                     -KAPPA/H2
 ND

OBJECT BOUND

*   Solution

*LO SOLTN                ??

ENDATA

***********************
* SET UP THE FUNCTION *
* AND RANGE ROUTINES  *
***********************

ELEMENTS      FLETCHBV

TEMPORARIES

 M  COS
 M  SIN

INDIVIDUALS

 T  COS
 F                      COS( V )
 G  V                   - SIN( V )
 H  V         V         - COS( V )

ENDATA

*********************
* SET UP THE GROUPS *
* ROUTINE           *
*********************

GROUPS        FLETCHBV

INDIVIDUALS

 T  HALFL2

 F                      5.0D-1 * GVAR * GVAR
 G                      GVAR
 H                      1.0D+0

ENDATA
