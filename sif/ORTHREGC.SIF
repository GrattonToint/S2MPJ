***************************
* SET UP THE INITIAL DATA *
***************************

NAME          ORTHREGC

*   Problem :
*   *********

*   An orthogonal regression problem,

*   The problem is to fit (orthogonally) an ellipse to a set of points
*   in the plane. This set of points is generated by perturbing  a
*   first set lying exactly on a predefined ellipse centered at the
*   origin.

*   Source:  adapted from:
*   M. Gulliksson,
*   "Algorithms for nonlinear Least-squares with Applications to
*   Orthogonal Regression",
*   UMINF-178.90, University of Umea, Sweden, 1990.

*   SIF input: Ph. Toint, June 1990.

*   classification QQR2-AN-V-V

*   Number of data points
*   (number of variables = 2 NPTS + 5 )

*IE NPTS                10             $-PARAMETER n= 25      original value
*IE NPTS                50             $-PARAMETER n= 105
*IE NPTS                250            $-PARAMETER n= 505
*IE NPTS                500            $-PARAMETER n= 1005
*IE NPTS                2500           $-PARAMETER n= 5005
 IE NPTS                10             $-PARAMETER     modified for S2X tests
*IE NPTS                5000           $-PARAMETER n= 10005
*IE NPTS                10000          $-PARAMETER n= 20005
*IE NPTS                50000          $-PARAMETER n= 100005

*   True ellipse parameters (centered at the origin)

 RE V11                 2.0
 RE V12                 1.0
 RE V22                 2.0

*   Perturbation parameters

 RE PSEED               237.1531
 RE PSIZE               0.2

*   Constants

 IE 1                   1
 IE 0                   0

 RE PI                  3.1415926535

*   Computed parameters

 RM 2PI       PI        2.0
 RI RNPTS     NPTS
 RD ICR0      RNPTS     1.0
 R* INCR      ICR0                     2PI
 R( C3        COS                      V22
 R( S3        SIN                      V22
 RM -S3       S3        -1.0

*   Construct the data points

 DO I         1                        NPTS
 IA I-1       I         -1
 RI RI-1      I-1
 R* THETA     RI-1                     INCR
 R( ST        SIN                      THETA
 R( CT        COS                      THETA
 R* U1        V11                      CT
 R* U2        V12                      ST
 R* U1C       U1                       C3
 R* U2-S      U2                       -S3
 R+ R1        U1C                      U2-S
 R* U1S       U1                       S3
 R* U2C       U2                       C3
 R+ R2        U1S                      U2C
 R* XSEED     THETA                    PSEED
 R( SSEED     COS                      XSEED
 R* PER-1     PSIZE                    SSEED
 RA PERT      PER-1      1.0
 A* XD(I)     R1                       PERT
 A* YD(I)     R2                       PERT
 ND

VARIABLES

*   Parameters of the ellipse

    H11
    H12
    H22
    G1
    G2

 DO I         1                        NPTS

*   Projections of the data points onto the ellipse

 X  X(I)
 X  Y(I)

 ND

GROUPS

 DO I         1                        NPTS
 XN OX(I)     X(I)      1.0
 XN OY(I)     Y(I)      1.0
 XE E(I)
 ND

CONSTANTS

 DO I         1                        NPTS
 Z  ORTHREGC  OX(I)                    XD(I)
 Z  ORTHREGC  OY(I)                    YD(I)
 X  ORTHREGC  E(I)      1.0
 ND

BOUNDS

 FR ORTHREGC  'DEFAULT'

START POINT

    ORTHREGC  H11       1.0
    ORTHREGC  H12       0.0
    ORTHREGC  H22       1.0
    ORTHREGC  G1        1.0
    ORTHREGC  G2        1.0

 DO I         1                        NPTS
 Z  ORTHREGC  X(I)                     XD(I)
 Z  ORTHREGC  Y(I)                     YD(I)
 ND

ELEMENT TYPE

 EV HXX       H                        X

 EV HXY       H                        X
 EV HXY       Y

 EV GX        G                        X

ELEMENT USES

 DO I         1                        NPTS

 XT EA(I)     HXX
 ZV EA(I)     H                        H11
 ZV EA(I)     X                        X(I)

 XT EB(I)     HXY
 ZV EB(I)     H                        H12
 ZV EB(I)     X                        X(I)
 ZV EB(I)     Y                        Y(I)

 XT EC(I)     HXX
 ZV EC(I)     H                        H22
 ZV EC(I)     X                        Y(I)

 XT ED(I)     GX
 ZV ED(I)     G                        G1
 ZV ED(I)     X                        X(I)

 XT EE(I)     GX
 ZV EE(I)     G                        G2
 ZV EE(I)     X                        Y(I)

 ND

GROUP TYPE

 GV L2        GVAR

GROUP USES

 DO I         1                        NPTS

 XT OX(I)     L2
 XT OY(I)     L2

 XE E(I)      EA(I)                    EB(I)     2.0
 XE E(I)      EC(I)                    ED(I)     -2.0
 XE E(I)      EE(I)     -2.0

 ND

OBJECT BOUND

 LO ORTHREGC            0.0

*   Solution

*LO SOLTN(10)           0.399058640
*LO SOLTN(50)           1.975569984
*LO SOLTN(250)          9.581964015
*LO SOLTN(500)          18.79064716
*LO SOLTN(2500)         ???
*LO SOLTN(5000)         ???

ENDATA

***********************
* SET UP THE FUNCTION *
* AND RANGE ROUTINES  *
***********************

ELEMENTS      ORTHREGC

INDIVIDUALS

 T  HXX
 F                      H * X * X
 G  H                   X * X
 G  X                   2.0 * H * X
 H  H         X         X + X
 H  X         X         H + H

 T  HXY
 F                      H * X * Y
 G  H                   X * Y
 G  X                   H * Y
 G  Y                   H * X
 H  H         X         Y
 H  H         Y         X
 H  X         Y         H

 T  GX
 F                      G * X
 G  G                   X
 G  X                   G
 H  G         X         1.0

ENDATA

*********************
* SET UP THE GROUPS *
* ROUTINE           *
*********************

GROUPS        ORTHREGC

*   Least-square groups

INDIVIDUALS

 T  L2
 F                      GVAR * GVAR
 G                      GVAR + GVAR
 H                      2.0

ENDATA
