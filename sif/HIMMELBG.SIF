***************************
* SET UP THE INITIAL DATA *
***************************

NAME          HIMMELBG

*   Problem :
*   *********

*   A 2 variables problem by Himmelblau.

*   Source: problem 33 in
*   D.H. Himmelblau,
*   "Applied nonlinear programming",
*   McGraw-Hill, New-York, 1972.

*   See Buckley#87 (p. 67)

*   SIF input: Ph. Toint, Dec 1989.

*   classification OUR2-AN-2-0

VARIABLES

    X1
    X2

GROUPS

 N  G

BOUNDS

 FR HIMMELBG  'DEFAULT'

START POINT

 XV HIMMELBG  'DEFAULT' 0.5

ELEMENT TYPE

 EV HG        X                        Y

ELEMENT USES

 T  E         HG
 V  E         X                        X1
 V  E         Y                        X2

GROUP USES

 E  G         E

OBJECT BOUND

 LO HIMMELBG            0.0

*   Solution

*LO SOLTN               0.0

ENDATA

***********************
* SET UP THE FUNCTION *
* AND RANGE ROUTINES  *
***********************

ELEMENTS      HIMMELBG

TEMPORARIES

 R  EX
 R  FC
 R  DFCDX
 R  DFCDY

 M  EXP

INDIVIDUALS

 T  HG
 A  EX                  EXP( - X - Y )
 A  FC                  2.0 * X * X + 3.0 * Y * Y
 A  DFCDX               4.0 * X
 A  DFCDY               6.0 * Y

 F                      EX * FC

 G  X                   EX * ( DFCDX - FC )
 G  Y                   EX * ( DFCDY - FC )

 H  X         X         EX * ( FC - 2.0 * DFCDX + 4.0 )
 H  X         Y         EX * ( FC - DFCDY - DFCDX )
 H  Y         Y         EX * ( FC - 2.0 * DFCDY + 6.0 )

ENDATA
