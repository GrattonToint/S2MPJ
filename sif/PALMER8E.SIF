***************************
* SET UP THE INITIAL DATA *
***************************

NAME          PALMER8E

*   Problem :
*   *********

*   A nonlinear least squares problem
*   arising from chemical kinetics.

*   model: H-N=C=Se TZVP + MP2
*   fitting Y to A0 + A2 X**2 + A4 X**4 + A6 X**6 + A8 X**8 +
*                A10 X**10 + L * EXP( -K X**2 )

*   Source:
*   M.  Palmer, Edinburgh, private communication.

*   SIF input: Nick Gould, 1992.

*   classification SBR2-RN-8-0
 
*   Number of data points

 IE M                   23

*   Other data

 IE 1                   1
 IE 12                  12

*   X (radians)

 RE X12                 0.000000    
 RE X13                 0.174533    
 RE X14                 0.314159    
 RE X15                 0.436332    
 RE X16                 0.514504    
 RE X17                 0.610865    
 RE X18                 0.785398    
 RE X19                 0.959931    
 RE X20                 1.134464    
 RE X21                 1.308997    
 RE X22                 1.483530    
 RE X23                 1.570796    

*   Y( KJmol-1)

 RE Y12                  4.757534
 RE Y13                  3.121416
 RE Y14                  1.207606
 RE Y15                  0.131916
 RE Y16                  0.000000
 RE Y17                  0.258514
 RE Y18                  3.380161
 RE Y19                 10.762813
 RE Y20                 23.745996
 RE Y21                 44.471864
 RE Y22                 76.541947
 RE Y23                 97.874528

VARIABLES

    A0
    A2
    A4
    A6
    A8
    A10
    K
    L

GROUPS

 DO I         12                       M
 A* XSQR      X(I)                     X(I)
 A* XQUART    XSQR                     XSQR
 A* X**6      XSQR                     XQUART
 A* X**8      XSQR                     X**6
 A* X**10     XSQR                     X**8
 A* X**12     XSQR                     X**10
 A* X**14     XSQR                     X**12
 XN O(I)      A0        1.0
 ZN O(I)      A2                       XSQR
 ZN O(I)      A4                       XQUART
 ZN O(I)      A6                       X**6
 ZN O(I)      A8                       X**8
 ZN O(I)      A10                      X**10
 ND

CONSTANTS

 DO I         12                       M
 Z  PALMER8E  O(I)                     Y(I)
 ND

BOUNDS

 FR PALMER8E  A0
 FR PALMER8E  A2
 FR PALMER8E  A4
 FR PALMER8E  A6
 FR PALMER8E  A8
 FR PALMER8E  A10
 FR PALMER8E  L

START POINT

 XV PALMER8E  'DEFAULT' 1.0

ELEMENT TYPE

 EV PROD      K                        L
 EP PROD      XSQR

ELEMENT USES

 DO I         12                       M
 A* XSQR      X(I)                     X(I)

 XT E(I)      PROD
 ZV E(I)      K                        K
 ZV E(I)      L                        L
 ZP E(I)      XSQR                     XSQR

 ND

GROUP TYPE

 GV L2        GVAR

GROUP USES

 DO I         12                       M
 XT O(I)      L2
 XE O(I)      E(I)
 ND

OBJECT BOUND

*   Least square problems are bounded below by zero

 LO PALMER8E                0.0

*   Solution

*LO SOLTN              1.48003482D-04

ENDATA

***********************
* SET UP THE FUNCTION *
* AND RANGE ROUTINES  *
***********************

ELEMENTS      PALMER8E

TEMPORARIES

 R  EXPON
 M  EXP

INDIVIDUALS

 T  PROD
 A  EXPON               EXP( - K * XSQR )
 F                      L * EXPON
 G  K                   - XSQR * L * EXPON
 G  L                   EXPON
 H  K         K         XSQR * XSQR * L * EXPON
 H  K         L         - XSQR * EXPON

ENDATA

*********************
* SET UP THE GROUPS *
* ROUTINE           *
*********************

GROUPS        PALMER8E

INDIVIDUALS

 T  L2
 F                      GVAR * GVAR
 G                      GVAR + GVAR
 H                      2.0

ENDATA









