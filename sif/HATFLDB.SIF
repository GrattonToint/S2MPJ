***************************
* SET UP THE INITIAL DATA *
***************************

NAME          HATFLDB

*   Problem :
*   *********

*   A test problem from the OPTIMA user manual.

*   Source:
*   "The OPTIMA user manual (issue No.8, p. 12)",
*   Numerical Optimization Centre, Hatfield Polytechnic (UK), 1989.

*   SIF input: Ph. Toint, May 1990.

*   classification SBR2-AN-4-0

*   Number of variables

 IE N                   4

*   Constants

 IE 1                   1
 IE 2                   2

VARIABLES

 DO I         1                        N
 X  X(I)
 ND

GROUPS

 XN G(1)      X(1)      1.0

 DO I         2                        N
 IA I-1       I         -1
 XN G(I)      X(I-1)    1.0
 ND

CONSTANTS

    HATFLDB   G1        1.0

BOUNDS

 XL HATFLDB   'DEFAULT' 0.0000001
 UP HATFLDB   X2        0.8

START POINT

 XV HATFLDB   'DEFAULT' 0.1

ELEMENT TYPE

 EV SQR       X

ELEMENT USES

 XT 'DEFAULT' SQR

 DO I         2                        N
 ZV E(I)      X                        X(I)
 ND

GROUP TYPE

 GV L2        GVAR

GROUP USES

 XT 'DEFAULT' L2

 DO I         2                        N
 XE G(I)      E(I)      -1.0
 ND

OBJECT BOUND

 LO HATFLDB             0.0

*   Solution

*LO SOLTN               5.57281D-03

ENDATA

***********************
* SET UP THE FUNCTION *
* AND RANGE ROUTINES  *
***********************

ELEMENTS      HATFLDB

TEMPORARIES

 R  ROOT
 M  SQRT

INDIVIDUALS

 T  SQR
 A  ROOT                SQRT( X )
 F                      ROOT
 G  X                   0.5 / ROOT
 H  X         X         -0.25 / ( ROOT * X )

ENDATA

*********************
* SET UP THE GROUPS *
* ROUTINE           *
*********************

GROUPS        HATFLDB

INDIVIDUALS

 T  L2

 F                      GVAR * GVAR
 G                      GVAR + GVAR
 H                      2.0

ENDATA
