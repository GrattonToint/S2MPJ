***************************
* SET UP THE INITIAL DATA *
***************************

NAME          PALMER3E

*   Problem :
*   *********

*   A nonlinear least squares problem
*   arising from chemical kinetics.

*    model: H-N=C=S TZVP + MP2
*   fitting Y to A0 + A2 X**2 + A4 X**4 + A6 X**6 + A8 X**8 +
*                A10 X**10 + L * EXP( -K X**2 )

*   Source:
*   M. Palmer, Edinburgh, private communication.

*   SIF input: Nick Gould, 1990.

*   classification SBR2-RN-8-0

*   Number of data points

 IE M                   23

*   Other data

 IE 1                   1

*   X (radians)

 RE X1                   -1.658063
 RE X2                   -1.570796
 RE X3                   -1.396263
 RE X4                   -1.221730
 RE X5                   -1.047198
 RE X6                   -0.872665
 RE X7                   -0.766531
 RE X8                   -0.698132
 RE X9                   -0.523599
 RE X10                  -0.349066
 RE X11                  -0.174533
 RE X12                  0.0
 RE X13                  0.174533
 RE X14                  0.349066
 RE X15                  0.523599
 RE X16                  0.698132
 RE X17                  0.766531
 RE X18                  0.872665
 RE X19                  1.047198
 RE X20                  1.221730
 RE X21                  1.396263
 RE X22                  1.570796
 RE X23                  1.658063

*   Y( KJmol-1)

 RE Y1                  64.87939
 RE Y2                  50.46046
 RE Y3                  28.2034
 RE Y4                  13.4575
 RE Y5                  4.6547
 RE Y6                  0.59447
 RE Y7                  0.0000
 RE Y8                  0.2177
 RE Y9                  2.3029
 RE Y10                 5.5191
 RE Y11                 8.5519
 RE Y12                 9.8919
 RE Y13                 8.5519
 RE Y14                 5.5191
 RE Y15                 2.3029
 RE Y16                 0.2177
 RE Y17                 0.0000
 RE Y18                 0.59447
 RE Y19                 4.6547
 RE Y20                 13.4575
 RE Y21                 28.2034
 RE Y22                 50.46046
 RE Y23                 64.87939

VARIABLES

    A0
    A2
    A4
    A6
    A8
    A10
    K
    L

GROUPS

 DO I         1                        M
 A* XSQR      X(I)                     X(I)
 A* XQUART    XSQR                     XSQR
 A* X**6      XSQR                     XQUART
 A* X**8      XSQR                     X**6
 A* X**10     XSQR                     X**8
 A* X**12     XSQR                     X**10
 A* X**14     XSQR                     X**12
 XN O(I)      A0        1.0
 ZN O(I)      A2                       XSQR
 ZN O(I)      A4                       XQUART
 ZN O(I)      A6                       X**6
 ZN O(I)      A8                       X**8
 ZN O(I)      A10                      X**10
 ND

CONSTANTS

 DO I         1                        M
 Z  PALMER3E  O(I)                     Y(I)
 ND

BOUNDS

 FR PALMER3E  A0
 FR PALMER3E  A2
 FR PALMER3E  A4
 FR PALMER3E  A6
 FR PALMER3E  A8
 FR PALMER3E  A10
 FR PALMER3E  L

START POINT

 XV PALMER3E  'DEFAULT' 1.0

ELEMENT TYPE

 EV PROD      K                        L
 EP PROD      XSQR

ELEMENT USES

 DO I         1                        M
 A* XSQR      X(I)                     X(I)

 XT E(I)      PROD
 ZV E(I)      K                        K
 ZV E(I)      L                        L
 ZP E(I)      XSQR                     XSQR

 ND

GROUP TYPE

 GV L2        GVAR

GROUP USES

 DO I         1                        M
 XT O(I)      L2
 XE O(I)      E(I)
 ND

OBJECT BOUND

*   Least square problems are bounded below by zero

 LO PALMER3E               0.0

*   Solution

*LO SOLTN               5.0741053D-05

ENDATA

***********************
* SET UP THE FUNCTION *
* AND RANGE ROUTINES  *
***********************

ELEMENTS      PALMER3E

TEMPORARIES

 R  EXPON
 M  EXP

INDIVIDUALS

 T  PROD
 A  EXPON               EXP( - K * XSQR )
 F                      L * EXPON
 G  K                   - XSQR * L * EXPON
 G  L                   EXPON
 H  K         K         XSQR * XSQR * L * EXPON
 H  K         L         - XSQR * EXPON

ENDATA

*********************
* SET UP THE GROUPS *
* ROUTINE           *
*********************

GROUPS        PALMER3E

INDIVIDUALS

 T  L2
 F                      GVAR * GVAR
 G                      GVAR + GVAR
 H                      2.0

ENDATA
