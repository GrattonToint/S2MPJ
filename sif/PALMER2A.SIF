***************************
* SET UP THE INITIAL DATA *
***************************

NAME          PALMER2A

*   Problem :
*   *********

*   A nonlinear least squares problem with bounds
*   arising from chemical kinetics.

*   model: H-N=C=O TZVP + MP2
*   fitting Y to A0 + A2 X**2 + A4 X**4 + A6 X**6
*                + B / ( C + X**2 ), B, C nonnegative.

*   Source:
*   M. Palmer, Edinburgh, private communication.

*   SIF input: Nick Gould, 1990.

*   classification SBR2-RN-6-0

*   Number of data points

 IE M                   23

*   Other data

 IE 1                   1

*   X (radians)

 RE X1                   -1.745329
 RE X2                   -1.570796
 RE X3                   -1.396263
 RE X4                   -1.221730
 RE X5                   -1.047198
 RE X6                   -0.937187
 RE X7                   -0.872665
 RE X8                   -0.698132
 RE X9                   -0.523599
 RE X10                  -0.349066
 RE X11                  -0.174533
 RE X12                  0.0
 RE X13                  0.174533
 RE X14                  0.349066
 RE X15                  0.523599
 RE X16                  0.698132
 RE X17                  0.872665
 RE X18                  0.937187
 RE X19                  1.047198
 RE X20                  1.221730
 RE X21                  1.396263
 RE X22                  1.570796
 RE X23                  1.745329

*   Y( KJmol-1)

 RE Y1                  72.676767
 RE Y2                  40.149455
 RE Y3                  18.8548
 RE Y4                  6.4762
 RE Y5                  0.8596
 RE Y6                  0.00000
 RE Y7                  0.2730
 RE Y8                  3.2043
 RE Y9                  8.1080
 RE Y10                 13.4291
 RE Y11                 17.7149
 RE Y12                 19.4529
 RE Y13                 17.7149
 RE Y14                 13.4291
 RE Y15                 8.1080
 RE Y16                 3.2053
 RE Y17                 0.2730
 RE Y18                 0.00000
 RE Y19                 0.8596
 RE Y20                 6.4762
 RE Y21                 18.8548
 RE Y22                 40.149455
 RE Y23                 72.676767


VARIABLES

    A0
    A2
    A4
    A6
    B
    C

GROUPS

 DO I         1                        M
 A* XSQR      X(I)                     X(I)
 A* XQUART    XSQR                     XSQR
 A* XSEXT     XQUART                   XSQR
 XN O(I)      A0        1.0
 ZN O(I)      A2                       XSQR
 ZN O(I)      A4                       XQUART
 ZN O(I)      A6                       XSEXT
 ND

CONSTANTS

 DO I         1                        M
 Z  PALMER2A  O(I)                     Y(I)
 ND

BOUNDS

 FR PALMER2A  A0
 FR PALMER2A  A2
 FR PALMER2A  A4
 FR PALMER2A  A6
 LO PALMER2A  B         0.00001
 LO PALMER2A  C         0.00001

START POINT

 XV PALMER2A  'DEFAULT' 1.0

ELEMENT TYPE

 EV QUOT      B                        C
 EP QUOT      XSQR

ELEMENT USES

 DO I         1                        M
 A* XSQR      X(I)                     X(I)
 XT E(I)      QUOT
 ZV E(I)      B                        B
 ZV E(I)      C                        C
 ZP E(I)      XSQR                     XSQR
 ND

GROUP TYPE

 GV L2        GVAR

GROUP USES

 DO I         1                        M
 XT O(I)      L2
 XE O(I)      E(I)
 ND

OBJECT BOUND

*   Least square problems are bounded below by zero

 LO PALMER2A               0.0

*   Solution

*LO SOLTN               1.7109717D-02

ENDATA

***********************
* SET UP THE FUNCTION *
* AND RANGE ROUTINES  *
***********************

ELEMENTS      PALMER2A

TEMPORARIES

 R  DENOM

INDIVIDUALS

 T  QUOT
 A  DENOM               1.0 / ( C + XSQR )
 F                      B * DENOM
 G  B                   DENOM
 G  C                   - B * DENOM * DENOM
 H  B         C         - DENOM * DENOM
 H  C         C         2.0 * B * DENOM ** 3

ENDATA

*********************
* SET UP THE GROUPS *
* ROUTINE           *
*********************

GROUPS        PALMER2A

INDIVIDUALS

 T  L2
 F                      GVAR * GVAR
 G                      GVAR + GVAR
 H                      2.0

ENDATA
