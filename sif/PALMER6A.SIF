***************************
* SET UP THE INITIAL DATA *
***************************

NAME          PALMER6A

*   Problem :
*   *********

*   A nonlinear least squares problem with bounds
*   arising from chemical kinetics.

*   model: H-N=C=Se TZVP + MP2
*   fitting Y to A0 + A2 X**2 + A4 X**4 + A6 X**6
*                + B / ( C + X**2 ), B, C nonnegative.

*   Source:
*   M. Palmer, Edinburgh, private communication.

*   SIF input: Nick Gould, 1992.

*   classification SBR2-RN-6-0

*   Number of data points

 IE M                   24

*   Other data

 IE 1                   1
 IE 12                  12

*   X (radians)

 RE X12                 0.000000  
 RE X13                 1.570796  
 RE X14                 1.396263  
 RE X15                 1.221730  
 RE X16                 1.047198  
 RE X17                 0.872665  
 RE X18                 0.785398  
 RE X19                 0.732789  
 RE X20                 0.698132  
 RE X21                 0.610865  
 RE X22                 0.523599  
 RE X23                 0.349066  
 RE X24                 0.174533

*   Y( KJmol-1)

 RE Y12                 10.678659
 RE Y13                 75.414511
 RE Y14                 41.513459
 RE Y15                 20.104735
 RE Y16                  7.432436
 RE Y17                  1.298082
 RE Y18                  0.171300
 RE Y19                  0.000000
 RE Y20                  0.068203
 RE Y21                  0.774499
 RE Y22                  2.070002
 RE Y23                  5.574556
 RE Y24                  9.026378


VARIABLES

    A0
    A2
    A4
    A6
    B
    C

GROUPS

 DO I         12                       M
 A* XSQR      X(I)                     X(I)
 A* XQUART    XSQR                     XSQR
 A* XSEXT     XQUART                   XSQR
 XN O(I)      A0        1.0
 ZN O(I)      A2                       XSQR
 ZN O(I)      A4                       XQUART
 ZN O(I)      A6                       XSEXT
 ND

CONSTANTS

 DO I         12                       M
 Z  PALMER6A  O(I)                     Y(I)
 ND

BOUNDS

 FR PALMER6A  A0
 FR PALMER6A  A2
 FR PALMER6A  A4
 FR PALMER6A  A6
 LO PALMER6A  B         0.00001
 LO PALMER6A  C         0.00001

START POINT

 XV PALMER6A  'DEFAULT' 1.0

ELEMENT TYPE

 EV QUOT      B                        C
 EP QUOT      XSQR

ELEMENT USES

 DO I         12                       M
 A* XSQR      X(I)                     X(I)
 XT E(I)      QUOT
 ZV E(I)      B                        B
 ZV E(I)      C                        C
 ZP E(I)      XSQR                     XSQR
 ND

GROUP TYPE

 GV L2        GVAR

GROUP USES

 DO I         12                       M
 XT O(I)      L2
 XE O(I)      E(I)
 ND

OBJECT BOUND

*   Least square problems are bounded below by zero

 LO PALMER6A               0.0

*   Solution

*LO SOLTN               4.0606141D-02

ENDATA

***********************
* SET UP THE FUNCTION *
* AND RANGE ROUTINES  *
***********************

ELEMENTS      PALMER6A

TEMPORARIES

 R  DENOM

INDIVIDUALS

 T  QUOT
 A  DENOM               1.0 / ( C + XSQR )
 F                      B * DENOM
 G  B                   DENOM
 G  C                   - B * DENOM * DENOM
 H  B         C         - DENOM * DENOM
 H  C         C         2.0 * B * DENOM ** 3

ENDATA

*********************
* SET UP THE GROUPS *
* ROUTINE           *
*********************

GROUPS        PALMER6A

INDIVIDUALS

 T  L2
 F                      GVAR * GVAR
 G                      GVAR + GVAR
 H                      2.0

ENDATA
