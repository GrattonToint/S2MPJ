***************************
* SET UP THE INITIAL DATA *
***************************

NAME          HS50

*   Problem :
*   *********

*   Source: problem 50 in
*   W. Hock and K. Schittkowski,
*   "Test examples for nonlinear programming codes",
*   Lectures Notes in Economics and Mathematical Systems 187, Springer
*   Verlag, Heidelberg, 1981.

*   SIF input: A.R. Conn, April 1990
*   updated to improve numerics, Nick Gould, Oct 2003

*   classification OLR2-AY-5-3

*   some useful parameters, including N, the number of variables.

 IE N                   5

 IE 1                   1
 IE 2                   2
 IE 3                   3
 IE 4                   4

VARIABLES

 DO I         1                        N
 X  X(I)
 ND

GROUPS

 N  OBJ

 DO I         1                        3
 IA J         I         1
 IA K         J         1
 XE CON(I)    X(I)      1.0            X(J)      2.0
 XE CON(I)    X(K)      3.0
 ND

CONSTANTS

    HS50      OBJ       0.0
    HS50      CON1      6.0
    HS50      CON2      6.0
    HS50      CON3      6.0

BOUNDS

 FR HS50      'DEFAULT'

START POINT

    HS50      X1        35.0
    HS50      X2        -31.0
    HS50      X3        11.0
    HS50      X4        5.0
    HS50      X5        -5.0

ELEMENT TYPE

 EV DIFF2     V1                       V2
 IV DIFF2     U
 EV DIFF4     V1                       V2
 IV DIFF4     U

ELEMENT USES

 XT E1        DIFF2
 ZV E1        V1                       X1
 ZV E1        V2                       X2  

 XT E2        DIFF2
 ZV E2        V1                       X2
 ZV E2        V2                       X3  

 XT E3        DIFF4
 ZV E3        V1                       X3
 ZV E3        V2                       X4  

 XT E4        DIFF2
 ZV E4        V1                       X4
 ZV E4        V2                       X5  

GROUP USES

 E  OBJ       E1                       E2
 E  OBJ       E3                       E4

OBJECT BOUND

 LO HS50                0.0

*   Solution

*LO SOLTN               0.0

ENDATA

***********************
* SET UP THE FUNCTION *
* AND RANGE ROUTINES  *
***********************

ELEMENTS      HS50

TEMPORARIES

INDIVIDUALS

 T  DIFF2
 R  U         V1        1.0            V2        -1.0
 F                      U ** 2
 G  U                   U + U
 H  U         U         2.0

 T  DIFF4
 R  U         V1        1.0            V2        -1.0
 F                      U ** 4
 G  U                   4.0 * U ** 3
 H  U         U         12.0 * U ** 2

ENDATA
