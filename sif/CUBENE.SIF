***************************
* SET UP THE INITIAL DATA *
***************************

NAME          CUBENE

*   Problem :
*   *********

*   A cubic variant of the Rosenbrock test function.
*   Nonlinear equations version.

*   Source: problem 5 (p. 89) in
*   A.R. Buckley,
*   "Test functions for unconstrained minimization",
*   TR 1989CS-3, Mathematics, statistics and computing centre,
*   Dalhousie University, Halifax (CDN), 1989.

*   SIF input: Ph. Toint, Dec 1989.

*   classification NOR2-AN-2-2

*   dimension

 IE N                           2

*   other parameter definitions

 IE 1                           1
 IE 2                           2
 IA N-1       N                -1

VARIABLES

 DO I         1                        N
 X  X(I)
 ND

GROUPS

 XE SQ(1)     X(1)             1.0

 DO I         2                        N
 XE SQ(I)     'SCALE'          0.1
 XE SQ(I)     X(I)             1.0
 ND

CONSTANTS

    CUBENE    SQ1              1.0

BOUNDS

 FR CUBENE    'DEFAULT'

START POINT

    CUBENE    X1              -1.2
    CUBENE    X2               1.0

ELEMENT TYPE

 EV ETYPE     V1

ELEMENT USES

 XT 'DEFAULT' ETYPE

 DO I         2                        N
 IA I-1       I         -1
 ZV ELA(I)    V1                       X(I-1)
 ND

GROUP USES

 DO I         2                        N
 XE SQ(I)     ELA(I)
 ND

OBJECT BOUND

 LO CUBENE              0.0

*   Solution

*LO SOLTN               0.0

ENDATA

***********************
* SET UP THE FUNCTION *
* AND RANGE ROUTINES  *
***********************

ELEMENTS      CUBENE

INDIVIDUALS

 T  ETYPE
 F                      - V1 ** 3
 G  V1                  - 3.0 * V1 ** 2
 H  V1        V1        - 6.0 * V1 

ENDATA
