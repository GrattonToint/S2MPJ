***************************
* SET UP THE INITIAL DATA *
***************************

NAME          SCOSINE 

*   Problem :
*   *********

*   Another function with nontrivial groups and
*   repetitious elements.
*   NB: scaled version of COSINE

*   Source:
*   N. Gould, private communication.

*   SIF input: N. Gould, Nov 1997

*   classification OUR2-AN-V-0

*   number of variables

*IE N                   10             $-PARAMETER
*IE N                   100            $-PARAMETER
*IE N                   1000           $-PARAMETER    original value
*IE N                   5000           $-PARAMETER
 IE N                   10             $-PARAMETER     modified for S2X tests
*IE N                   10000          $-PARAMETER

*  ratio of smallest to largest scale factors will be exp(scal)

 RE SCAL                12.0

*   other parameter definitions

 IE 1                   1
 IA N-1       N         -1
 RI RN-1      N-1          
 RM -RN-1     RN-1      -1.0

 RE ONE                 1.0
 RI RN        N
 RA RN-1      RN        -1

VARIABLES

 DO I         1                        N
 IA I-1       I         -1
 RI RI-1      I-1
 R/ RAT       RI-1                     RN-1
 R* ARG       RAT                      SCAL
 A( SCALE(I)  EXP                      ARG
 X  X(I)
 ND

GROUPS

*  objective function

 DO I         1                        N-1
 IA I+1       I         1
 AM MULT      SCALE(I+1)-0.5
 ZN G(I)      X(I+1)                   MULT
 OD

BOUNDS

 FR SCOSINE   'DEFAULT'

START POINT

 DO I         1                        N
 A/ DIV       ONE                      SCALE(I)
 ZV SCOSINE   X(I)                     DIV
 ND

ELEMENT TYPE

 EV SQ        V
 EP SQ        P

ELEMENT USES

 DO I         1                        N-1

 XT E(I)      SQ
 ZV E(I)      V                        X(I)
 ZP E(I)      P                        SCALE(I)

 ND

GROUP TYPE

 GV COS       GVAR

GROUP USES

 DO I         1                        N-1
 XT G(I)      COS
 XE G(I)      E(I)
 ND

OBJECT BOUND

 ZL SCOSINE                            -RN-1

*   Solution

*LO SOLTN               ???

ENDATA

***********************
* SET UP THE FUNCTION *
* AND RANGE ROUTINES  *
***********************

ELEMENTS      SCOSINE 

TEMPORARIES

 R  PP

INDIVIDUALS

 T  SQ
 A  PP                  P * P
 F                      PP * V * V
 G  V                   PP * ( V + V )
 H  V         V         2.0 * PP

ENDATA

*********************
* SET UP THE GROUPS *
* ROUTINE           *
*********************

GROUPS        SCOSINE 

TEMPORARIES

 R  COSG
 M  SIN
 M  COS

INDIVIDUALS

 T  COS
 A  COSG                COS( GVAR )
 F                      COSG
 G                      - SIN( GVAR )
 H                      - COSG

ENDATA
