
***************************
* SET UP THE INITIAL DATA *
***************************

NAME          FLETCHCR

*   Problem :
*   --------

*   The chained Rosenbrock function as given by Fletcher.

*   Source:  The second problem given by
*   R. Fletcher,
*   "An optimal positive definite update for sparse Hessian matrices"
*   Numerical Analysis report NA/145, University of Dundee, 1992.

*   SIF input: Nick Gould, Oct 1992.

*   classification OUR2-AN-V-0

*   The Number of variables is N.

*IE N                   10             $-PARAMETER     original value
*IE N                   100            $-PARAMETER
*IE N                   1000           $-PARAMETER
 IE N                   10             $-PARAMETER     modified for S2X tests

*   other parameter definitions

 IE 1                   1
 IA N-1       N         -1

VARIABLES

 DO I         1                        N
 X  X(I)
 ND

GROUPS

 DO I         1                        N-1
 IA I+1       I         1
 XN SQ1(I)    X(I+1)    1.0
 XN SQ1(I)    'SCALE'   0.01
 XN SQ2(I)    X(I)      -1.0
 ND

CONSTANTS

 DO I         1                        N-1
 X  FLETCHCR  SQ2(I)    -1.0
 ND

BOUNDS

 FR FLETCHCR  'DEFAULT'

START POINT

 XV FLETCHCR  'DEFAULT' 0.0

ELEMENT TYPE

 EV ETYPE     V1

ELEMENT USES

 XT 'DEFAULT' ETYPE

 DO I         1                        N-1
 ZV SQ1(I)    V1                       X(I)
 ND

GROUP TYPE

 GV L2        GVAR

GROUP USES

 XT 'DEFAULT' L2

 DO I         1                        N-1
 XE SQ1(I)    SQ1(I)    -1.0
 ND

OBJECT BOUND

 LO FLETCHCR            0.0

*   Solution

*LO SOLTN               0.0

ENDATA

***********************
* SET UP THE FUNCTION *
* AND RANGE ROUTINES  *
***********************

ELEMENTS      FLETCHCR

INDIVIDUALS

 T  ETYPE
 F                      V1 ** 2
 G  V1                  2.0D+0 * V1
 H  V1        V1        2.0D+0

ENDATA

*********************
* SET UP THE GROUPS *
* ROUTINE           *
*********************

GROUPS        FLETCHCR

INDIVIDUALS

 T  L2
 F                      GVAR * GVAR
 G                      GVAR + GVAR
 H                      2.0D+0

ENDATA
