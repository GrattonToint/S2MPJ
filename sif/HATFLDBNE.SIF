***************************
* SET UP THE INITIAL DATA *
***************************

NAME          HATFLDBNE

*   Problem :
*   *********

*   A test problem from the OPTIMA user manual.

*   Source:
*   "The OPTIMA user manual (issue No.8, p. 12)",
*   Numerical Optimization Centre, Hatfield Polytechnic (UK), 1989.

*   SIF input: Ph. Toint, May 1990.
*   Bound-constrained nonlinear equations version: Nick Gould, June 2019.

*   classification NOR2-AN-4-4

*   Number of variables

 IE N                   4

*   Constants

 IE 1                   1
 IE 2                   2

VARIABLES

 DO I         1                        N
 X  X(I)
 ND

GROUPS

 XE G(1)      X(1)      1.0

 DO I         2                        N
 IA I-1       I         -1
 XE G(I)      X(I-1)    1.0
 ND

CONSTANTS

    HATFLDB   G1        1.0

BOUNDS

 XL HATFLDB   'DEFAULT' 0.0000001
 UP HATFLDB   X2        0.8

START POINT

 XV HATFLDB   'DEFAULT' 0.1

ELEMENT TYPE

 EV SQR       X

ELEMENT USES

 XT 'DEFAULT' SQR

 DO I         2                        N
 ZV E(I)      X                        X(I)
 ND

GROUP USES

 DO I         2                        N
 XE G(I)      E(I)      -1.0
 ND

OBJECT BOUND

*LO HATFLDB             0.0

*   Solution

*LO SOLTN               5.57281D-03

ENDATA

***********************
* SET UP THE FUNCTION *
* AND RANGE ROUTINES  *
***********************

ELEMENTS      HATFLDBNE

TEMPORARIES

 R  ROOT
 M  SQRT

INDIVIDUALS

 T  SQR
 A  ROOT                SQRT( X )
 F                      ROOT
 G  X                   0.5 / ROOT
 H  X         X         -0.25 / ( ROOT * X )

ENDATA
