***************************
* SET UP THE INITIAL DATA *
***************************

NAME          LUKVLE3 

*   Problem :
*   *********

*   Source: Problem 5.3, the chained Powell singular function with 
*   simplified trigonometric exponential constraints, 
*   due to L. Luksan and J. Vlcek,
*   "Sparse and partially separable test problems for 
*   unconstrained and equality constrained optimization",
*   Technical Report 767, Inst. Computer Science, Academy of Sciences
*   of the Czech Republic, 182 07 Prague, Czech Republic, 1999

*   SIF input: Nick Gould, April 2001

*   classification OOR2-AY-V-V

*   some useful parameters, including N, the number of variables.

*IE N                   100            $-PARAMETER
*IE N                   1000           $-PARAMETER
*IE N                   10000          $-PARAMETER
 IE N                   10             $-PARAMETER     modified for S2X tests
*IE N                   100000         $-PARAMETER

*  other useful parameters

 IE 1                   1
 IE 2                   2
 IE 3                   3
 IE 4                   4
 IE 5                   5
 IE 6                   6

 I/ N/2       N                        2
 IA N/2-1     N/2       -1
 IA N-1       N         -1

VARIABLES

 DO I         1                        N
 X  X(I)
 ND

GROUPS

 DO I         1                        N/2-1
 IM 2I        I         2
 IA 2I-1      2I        -1
 IA 2I+1      2I        1
 IA 2I+2      2I        2
 XN OA(I)     X(2I-1)   1.0            X(2I)     10.0
 XN OB(I)     X(2I+1)   1.0            X(2I+2)   -1.0
 XN OC(I)     X(2I)     1.0            X(2I+1)   -2.0
 XN OD(I)     X(2I-1)   1.0            X(2I+2)   -1.0
 ND

 XE C1        X(2)      2.0
 XE C2        X(N-1)    4.0

CONSTANTS

 X  RHS       C1        5.0
 X  RHS       C2        3.0

BOUNDS

 FR LUKVLE3   'DEFAULT'

START POINT

 DO I         1                        N
 DI I         4
 XV LUKVLE3   X(I)      3.0
 ND

 DO I         2                        N
 DI I         4
 XV LUKVLE3   X(I)      -1.0
 ND

 DO I         3                        N
 DI I         4
 XV LUKVLE3   X(I)      0.0
 ND

 DO I         4                        N
 DI I         4
 XV LUKVLE3   X(I)      1.0
 ND

ELEMENT TYPE

 EV CUBE      V
 EV XEXP      X1                       X2
 EV SINASINB  X1                       X2

ELEMENT USES

 XT C11       CUBE
 ZV C11       V                        X(1)
 XT C12       SINASINB
 ZV C12       X1                       X(1)
 ZV C12       X2                       X(2)
 XT C21       XEXP
 ZV C21       X1                       X(N-1)
 ZV C21       X2                       X(N)

GROUP TYPE

 GV L2P       GVAR 
 GP L2P       P
 GV L4P       GVAR
 GP L4P       P

GROUP USES

 DO I         1                        N/2-1
 XT OA(I)     L2P
 XP OA(I)     P         1.0    
 XT OB(I)     L2P
 XP OB(I)     P         5.0    
 XT OC(I)     L4P
 XP OC(I)     P         1.0    
 XT OD(I)     L4P
 XP OD(I)     P         10.0    
 ND

 XE C1        C11       3.0            C12
 XE C2        C21       -1.0

OBJECT BOUND

 LO LUKVLE3             0.0

*   Solution

*LO SOLTN               2.758658E+01

ENDATA

***********************
* SET UP THE FUNCTION *
* AND RANGE ROUTINES  *
***********************

ELEMENTS      LUKVLE3 

TEMPORARIES

 R  EXPX
 R  XEXPX
 R  SINA
 R  SINB
 R  COSA
 R  COSB
 M  EXP
 M  SIN
 M  COS

INDIVIDUALS

 T  XEXP
 A  EXPX                EXP( X1 - X2 )
 A  XEXPX               X1 * EXPX
 F                      XEXPX
 G  X1                  EXPX + XEXPX
 G  X2                  - XEXPX
 H  X1        X1        2.0 * EXPX + XEXPX
 H  X1        X2        - EXPX - XEXPX
 H  X2        X2        XEXPX

 T  SINASINB
 A  SINA                SIN( X1 - X2 )
 A  SINB                SIN( X1 + X2 )
 A  COSA                COS( X1 - X2 )
 A  COSB                COS( X1 + X2 )
 F                      SINA * SINB
 G  X1                  SINA * COSB + COSA * SINB
 G  X2                  SINA * COSB - COSA * SINB
 H  X1        X1        2.0 * ( COSA * COSB - SINA * SINB )
 H  X2        X2        - 2.0 * ( COSA * COSB + SINA * SINB )

 T  CUBE
 F                      V ** 3
 G  V                   3.0 * V ** 2
 H  V         V         6.0 * V

ENDATA

*********************
* SET UP THE GROUPS *
* ROUTINE           *
*********************

GROUPS        LUKVLE3 

INDIVIDUALS

 T  L2P
 F                      P * GVAR * GVAR
 G                      2.0 * P * GVAR
 H                      2.0 * P

 T  L4P
 F                      P * GVAR ** 4
 G                      4.0 * P * GVAR ** 3
 H                      12.0 * P * GVAR ** 2

ENDATA

