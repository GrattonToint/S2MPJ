***************************
* SET UP THE INITIAL DATA *
***************************

NAME          DIXCHLNV

*   Problem :
*   *********

*   A variant of a constrained problem set as a challenge for SQP methods
*   by L.C.W. Dixon at the APMOD91 Conference.
*   The variant from the original problem is that the variables have
*   been constrained to be above 1.0D-15, which in turn allows the
*   constraints to be expressed in terms of logarithms.

*   Source: a modification (by Ph. Toint) of
*   L.C.W. Dixon, personnal communication, Jan 1991.

*   SIF input: Ph. Toint, Feb 1991.

*   classification SOR2-AN-V-V

*   Number of variables
*   (variable, but must be even and at least equal to 4)

*IE N                   10             $-PARAMETER
*IE N                   50             $-PARAMETER
*IE N                   100            $-PARAMETER     original value
*IE N                   1000           $-PARAMETER
 IE N                   10             $-PARAMETER     modified for S2X tests
*IE N                   5000           $-PARAMETER

*   Other parameters

 IE 1                   1
 IE 2                   2
 IA N-3       N         -3
 IA N-1       N         -1
 RE 90.0                90.0
 RE 10.1                10.1
 RE 19.8                19.8
 RD 1/90.0    90.0      1.0
 RD 1/10.1    10.1      1.0
 RD 1/19.8    19.8      1.0

VARIABLES

 DO I         1                        N
 X  X(I)
 ND

GROUPS

*   Objective groups

 DO I         1                        N-3
 IA I+1       I         1
 IA I+2       I         2
 IA I+3       I         3

 XN A(I)      X(I+1)    1.0
 XN A(I)      'SCALE'   0.01

 XN B(I)      X(I)      1.0

 XN C(I)      X(I+3)    1.0
 ZN C(I)      'SCALE'                  1/90.0

 XN D(I)      X(I+2)    1.0

 XN E(I)      X(I+1)    1.0
 ZN E(I)      'SCALE'                  1/10.1

 XN F(I)      X(I+3)    1.0
 ZN F(I)      'SCALE'                  1/10.1

 XN G(I)
 ZN G(I)      'SCALE'                  1/19.8

 OD I

*   Constraints

 DO I         2                        N
 DI I         2
 XE P(I)
 OD I

CONSTANTS

 DO I         1                        N-3
 X  DIXCHLNV  B(I)      1.0
 X  DIXCHLNV  D(I)      1.0
 X  DIXCHLNV  E(I)      1.0
 X  DIXCHLNV  F(I)      1.0
 OD I

BOUNDS

 LO DIXCHLNV  'DEFAULT' 1.0D-8

START POINT

 RE X0                  2.0

 DO I         1                        N-1
 DI I         2
 ZV DIXCHLNV  X(I)                     X0
 RD 1/X0      X0        1.0
 IA I+1       I         1
 ZV DIXCHLNV  X(I+1)                   1/X0
 RA X0        X0        1.0
 OD I

ELEMENT TYPE

 EV SQ        V
 EV S2PR      V                        W
 EV LOGV      V

ELEMENT USES

 DO I         1                        N-1
 XT XSQ(I)    SQ
 ZV XSQ(I)    V                        X(I)
 OD I

 DO I         1                        N-3
 IA I+1       I         1
 IA I+3       I         3
 XT PR(I)     S2PR
 ZV PR(I)     V                        X(I+1)
 ZV PR(I)     W                        X(I+3)
 OD I

 DO I         1                        N
 XT LOGX(I)   LOGV
 ZV LOGX(I)   V                        X(I)
 OD I

GROUP TYPE

 GV L2        GVAR

GROUP USES

*   Objective groups

 DO I         1                        N-3
 IA I+2       I         2

 XT A(I)      L2
 XE A(I)      XSQ(I)    -1.0

 XT B(I)      L2

 XT C(I)      L2
 XE C(I)      XSQ(I+2)  -1.0

 XT D(I)      L2

 XT E(I)      L2

 XT F(I)      L2

 XE G(I)      PR(I)

 OD I

*   Constraints

 DO I         2                        N
 DI I         2
 DO J         1                        I
 XE P(I)      LOGX(J)
 OD J
 OD I

OBJECT BOUND

*   Solution

*LO SOLTN               0.0

ENDATA

***********************
* SET UP THE FUNCTION *
* AND RANGE ROUTINES  *
***********************

ELEMENTS      DIXCHLNV

TEMPORARIES

 M  LOG

INDIVIDUALS

 T  SQ
 F                      V * V
 G  V                   V + V
 H  V         V         2.0

 T  LOGV
 F                      LOG(V)
 G  V                   1.0 / V
 H  V         V         -1.0 / ( V * V )

 T  S2PR
 F                      ( V - 1.0 ) * ( W - 1.0 )
 G  V                   W - 1.0
 G  W                   V - 1.0
 H  V         W         1.0

ENDATA

*********************
* SET UP THE GROUPS *
* ROUTINE           *
*********************

GROUPS        DIXCHLNV

INDIVIDUALS

 T  L2
 F                      GVAR * GVAR
 G                      GVAR + GVAR
 H                      2.0

ENDATA
