***************************
* SET UP THE INITIAL DATA *
***************************

NAME          TQUARTIC

*   Problem :
*   *********

*   A quartic function with nontrivial groups and
*   repetitious elements.

*   Source:
*   Ph. Toint, private communication.

*   SIF input: Ph. Toint, Dec 1989.

*   classification SUR2-AN-V-0

*   number of variables

*IE N                   5              $-PARAMETER     original value
*IE N                   10             $-PARAMETER
*IE N                   50             $-PARAMETER
*IE N                   100            $-PARAMETER
*IE N                   500            $-PARAMETER
*IE N                   1000           $-PARAMETER
*IE N                   5000           $-PARAMETER
 IE N                   10             $-PARAMETER     modified for S2X tests
*IE N                   10000          $-PARAMETER

*   other parameter definitions

 IE 1                   1
 IE 2                   2

VARIABLES

 DO I         1                        N
 X  X(I)
 ND

GROUPS

*  objective function (1st group)

 N  G1        X1        1.0

*  objective function (remaining groups)

 DO I         2                        N
 XN G(I)
 ND

CONSTANTS

    TQUARTIC  G1        1.0

BOUNDS

 FR TQUARTIC  'DEFAULT'

START POINT

 XV TQUARTIC  'DEFAULT' 0.1

ELEMENT TYPE

 EV SQ        V1

ELEMENT USES

 T  E1        SQ
 ZV E1        V1                       X1

 DO I         2                        N
 XT E(I)      SQ
 ZV E(I)      V1                       X(I)
 ND

GROUP TYPE

 GV L2        GVAR

GROUP USES

 T  G1        L2

 DO I         2                        N
 XT G(I)      L2
 XE G(I)      E(I)      -1.0           E(1)
 ND

OBJECT BOUND

*   Least square problems are bounded below by zero

 LO TQUARTIC            0.0

*   Solution

*LO SOLTN               0.0

ENDATA

***********************
* SET UP THE FUNCTION *
* AND RANGE ROUTINES  *
***********************

ELEMENTS      TQUARTIC

INDIVIDUALS

 T  SQ
 F                      V1 * V1
 G  V1                  V1 + V1
 H  V1        V1        2.0

ENDATA

*********************
* SET UP THE GROUPS *
* ROUTINE           *
*********************

GROUPS        TQUARTIC

INDIVIDUALS

 T  L2
 F                      GVAR * GVAR
 G                      GVAR + GVAR
 H                      2.0


ENDATA
