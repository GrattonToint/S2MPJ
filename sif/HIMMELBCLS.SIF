***************************
* SET UP THE INITIAL DATA *
***************************

NAME          HIMMELBCLS

*   Problem :
*   *********

*   A 2 variables problem by Himmelblau.

*   Source: problem 28 in
*   D.H. Himmelblau,
*   "Applied nonlinear programming",
*   McGraw-Hill, New-York, 1972.

*   See Buckley#6 (p. 63)

*   SIF input: Ph. Toint, Dec 1989.
*   Least-sqaures variant, Nick Gould, Jan 2020.

*   classification OUR2-AN-2-0

VARIABLES

    X1
    X2

GROUPS

 N  G1        X2        1.0
 N  G2        X1        1.0

CONSTANTS

    HIMMELBC  G1        11.0
    HIMMELBC  G2        7.0

BOUNDS

 FR HIMMELBC  'DEFAULT'

START POINT

 XV HIMMELBC  'DEFAULT' 1.0

ELEMENT TYPE

 EV SQ        X

ELEMENT USES

 XT 'DEFAULT' SQ

 V  E1        X                        X1
 V  E2        X                        X2

GROUP TYPE

 GV L2        GVAR

GROUP USES

 T  'DEFAULT' L2

 XE G1        E1
 XE G2        E2

ENDATA

***********************
* SET UP THE FUNCTION *
* AND RANGE ROUTINES  *
***********************

ELEMENTS      HIMMELBCLS

INDIVIDUALS

 T  SQ
 F                      X * X
 G  X                   X + X
 H  X         X         2.0

ENDATA

*********************
* SET UP THE GROUPS *
* ROUTINE           *
*********************

GROUPS        HIMMELBCLS

INDIVIDUALS

 T  L2
 F                      GVAR * GVAR
 G                      GVAR + GVAR
 H                      2.0

ENDATA
