***************************
* SET UP THE INITIAL DATA *
***************************

NAME          GPP    

*   Problem :
*   *********

*   Example of a geometric programming problem.

*   Source:
*   Hans Mittelmann, private communication.

*   SIF input: N. Gould, Jan 1998

*   classification OOR2-AY-V-V

*   number of variables

*IE N                   250            $-PARAMETER     original value
*IE N                   500            $-PARAMETER
*IE N                   750            $-PARAMETER
*IE N                   1000           $-PARAMETER
 IE N                   100            $-PARAMETER     modified for S2X tests
*IE N                   1250           $-PARAMETER
*IE N                   1750           $-PARAMETER
*IE N                   2000           $-PARAMETER

*   other parameter definitions

 IE 1                   1
 IA N-1       N         -1

VARIABLES

 DO I         1                        N
 X  X(I)
 ND

GROUPS

*  objective function

 N  OBJ

*  constraints

 DO I         1                        N-1
 IA I+1       I         1
 XG INEQ1(I)  X(I)      1.0            X(I+1)    1.0
 XL INEQ2(I)
 OD

RHS

 DO I         1                        N-1
 X  GPP       INEQ2(I)  20.0
 OD

BOUNDS

 FR GPP       'DEFAULT'

START POINT

 XV GPP       'DEFAULT' 1.0

ELEMENT TYPE

 EV EXP       X
 EV EXPDIF    XI
 EV EXPDIF    XJ
 IV EXPDIF    X

ELEMENT USES

 DO I         1                        N
 XT E(I)      EXP
 ZV E(I)      X                        X(I)
 ND

 DO I         1                        N-1
 IA I+1       I         1
 DO J         I+1                      N
 XT E(I,J)    EXPDIF
 ZV E(I,J)    XI                       X(I)
 ZV E(I,J)    XJ                       X(J)
 ND

GROUP USES

 DO I         1                        N-1
 IA I+1       I         1
 XE INEQ2(I)  E(I)                     E(I+1)
 DO J         I+1                      N
 XE OBJ       E(I,J)
 ND

OBJECT BOUND

*   Solution

*XL SOLUTION             1.44009D+04   $ (N=250)

ENDATA

***********************
* SET UP THE FUNCTION *
* AND RANGE ROUTINES  *
***********************

ELEMENTS      GPP    

TEMPORARIES

 R  E
 M  EXP

INDIVIDUALS

 T  EXP
 A  E                   EXP( X )
 F                      E
 G  X                   E
 H  X         X         E

 T  EXPDIF
 R  X         XI        -1.0           XJ        1.0
 A  E                   EXP( X )
 F                      E
 G  X                   E
 H  X         X         E

ENDATA

