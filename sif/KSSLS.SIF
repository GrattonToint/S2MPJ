***************************
* SET UP THE INITIAL DATA *
***************************

NAME          KSSLS

*   Problem :
*   *********

*   The KSS system whose root at zero has exponential multiplicity
*   as a function of dimension.

*   Source:  problem 8.1 in
*   Wenrui Hao, Andrew J. Sommese and Zhonggang Zeng,
*   "An algorithm and software for computing multiplicity structures
*    at zeros of nonlinear systems", Technical Report,
*   Department of Applied & Computational Mathematics & Statistics
*   University of Notre Dame, Indiana, USA (2012)

*   SIF input: Nick Gould, Jan 2012.
*   Least-squares version of KSS.SIF, Nick Gould, Jan 2020.

*   classification SUR2-AN-V-0

*   Problem dimension

*IE N                   4              $-PARAMETER original value
*IE N                   10             $-PARAMETER
*IE N                   100            $-PARAMETER
*IE N                   1000           $-PARAMETER
 IE N                   10             $-PARAMETER     modified for S2X tests

*   Define useful parameters

 IE 1                   1
 RI RN        N
 RA RN-1      RN        -1.0

VARIABLES

 DO I         1                        N
 X  X(I)
 ND

GROUPS

 DO I         1                        N
 IA I-1       I         -1
 IA I+1       I         1
 DO II        1                        I-1
 XN E(I)      X(II)     1.0
 OD II
 XN E(I)      X(I)      -3.0
 DO II        I+1                      N
 XN E(I)      X(II)     1.0
 OD II
 OD I

CONSTANTS

 DO I         1                        N
 Z  KSS       E(I)                     RN-1
 ND

BOUNDS

 FR KSS       'DEFAULT'

START POINT

 X  KSS       'DEFAULT' 1000

ELEMENT TYPE

 EV SQ        X

ELEMENT USES

 DO I         1                        N
 XT S(I)      SQ
 ZV S(I)      X                        X(I)
 ND

GROUP TYPE

 GV L2        GVAR

GROUP USES

 XT 'DEFAULT' L2

 DO I         1                        N
 XE E(I)      S(I)
 ND

OBJECT BOUND

*   no objective

 LO KSS                 0.0

*   Solution

*LO SOLTN               0.0

ENDATA

***********************
* SET UP THE FUNCTION *
* AND RANGE ROUTINES  *
***********************

ELEMENTS      KSSLS

INDIVIDUALS

*   square elements

 T  SQ
 F                      X * X
 G  X                   X + X
 H  X         X         2.0

ENDATA

*********************
* SET UP THE GROUPS *
* ROUTINE           *
*********************

GROUPS        KSSLS

INDIVIDUALS

 T  L2
 F                      GVAR * GVAR
 G                      GVAR + GVAR
 H                      2.0

ENDATA
