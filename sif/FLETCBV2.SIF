
***************************
* SET UP THE INITIAL DATA *
***************************

NAME          FLETCBV2

*   Problem :
*   *********

*   Another Boundary Value problem.

*   Source:  The first problem given by
*   R. Fletcher,
*   "An optimal positive definite update for sparse Hessian matrices"
*   Numerical Analysis report NA/145, University of Dundee, 1992.
*   but assuming that the 1/h**2 term should read h**2
*   This is what Fletcher intended (private communication).

*   The author comments: "The problem arises from discretizing the bvp
*                  x"=-2+sin x in [0,1]
*    with x(0)=0, x(1)=1. This gives a symmetric system of equations,
*    the residual vector of which is the gradient of the given function."
*    He multiplies through by h^2 before integrating.

*   SIF input: Nick Gould, Nov 1992.

*   classification OUR2-AN-V-0

*   The number of variables is N.

*IE N                   10             $-PARAMETER     original value
*IE N                   100            $-PARAMETER
*IE N                   1000           $-PARAMETER
*IE N                   5000           $-PARAMETER
 IE N                   10             $-PARAMETER     modified for S2X tests
*IE N                   10000          $-PARAMETER

*  KAPPA a parameter.

 RE KAPPA               1.0            $-PARAMETER
*RE KAPPA               0.0            $-PARAMETER

*   Define useful parameters

 IE 0                   0
 IE 1                   1
 IE 2                   2
 RE 1.0                 1.0
 IA N-1       N         -1

 IA N+1       N         1
 RI RN+1      N+1
 R/ H         1.0                      RN+1
 R* H2        H                        H
 R* KAPPAH2   H2                       KAPPA
 RM -KAPPAH2  KAPPAH2   -1.0
 RM -2H2      H2        -2.0
 RA -1-2H2    -2H2      -1.0

VARIABLES

 DO I         1                        N
 X  X(I)
 ND

GROUPS

 XN G(0)      X(1)      1.0

 DO I         1                        N-1
 IA I+1       I         1
 XN G(I)      X(I)      1.0            X(I+1)    -1.0
 ND

 XN G(N)      X(N)       1.0

 DO I         1                        N-1
 ZN L(I)      X(I)                     -2H2
 ND

 ZN L(N)      X(N)                     -1-2H2

 DO I         1                        N
 ZN C(I)
 ND

BOUNDS

 FR FLETCBV2  'DEFAULT'

START POINT

 DO I         1                        N
 RI RI        I
 R* IH        RI                       H
 Z  FLETCBV2  X(I)                     IH
 ND

ELEMENT TYPE

 EV COS       V

ELEMENT USES

 T  'DEFAULT' COS

 DO I         1                        N
 ZV C(I)      V                        X(I)
 ND

GROUP TYPE

 GV HALFL2    GVAR

GROUP USES

 DO I         0                        N
 XT G(I)      HALFL2
 ND

 DO I         1                        N
 ZE C(I)      C(I)                     -KAPPAH2
 ND

OBJECT BOUND

*   Solution

*LO SOLTN                ??

ENDATA

***********************
* SET UP THE FUNCTION *
* AND RANGE ROUTINES  *
***********************

ELEMENTS      FLETCBV2

TEMPORARIES

 M  COS
 M  SIN

INDIVIDUALS

 T  COS
 F                      COS( V )
 G  V                   - SIN( V )
 H  V         V         - COS( V )

ENDATA

*********************
* SET UP THE GROUPS *
* ROUTINE           *
*********************

GROUPS        FLETCBV2

INDIVIDUALS

 T  HALFL2

 F                      5.0D-1 * GVAR * GVAR
 G                      GVAR
 H                      1.0D+0

ENDATA
