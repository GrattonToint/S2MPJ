***************************
* SET UP THE INITIAL DATA *
***************************

NAME          LUKVLI12

*   Problem :
*   *********

*   Source: Problem 5.12, the chained HS47 problem, 
*   due to L. Luksan and J. Vlcek,
*   "Sparse and partially separable test problems for 
*   unconstrained and equality constrained optimization",
*   Technical Report 767, Inst. Computer Science, Academy of Sciences
*   of the Czech Republic, 182 07 Prague, Czech Republic, 1999

*   Equality constraints changed to inequalities

*   SIF input: Nick Gould, April 2001

*   classification OQR2-AY-V-V

*   some useful parameters, including N, the number of variables.

*IE N                   97             $-PARAMETER
*IE N                   997            $-PARAMETER
*IE N                   9997           $-PARAMETER
 IE N                   7              $-PARAMETER     modified for S2X tests
*IE N                   99997          $-PARAMETER

*  other useful parameters

 IE 1                   1
 IE 2                   2
 IE 3                   3
 IE 4                   4
 IE 5                   5

 IA N-1       N         -1
 I/ (N-1)/4   N-1                      4
 I* NC        3                        (N-1)/4

VARIABLES

 DO I         1                        N
 X  X(I)
 ND

GROUPS

 DO I         1                        (N-1)/4
 IA I-1       I         -1
 I* J         4                        I-1
 IA J+1       J         1
 IA J+2       J         2
 IA J+3       J         3
 IA J+4       J         4
 IA J+5       J         5
 XN OBJ1(I)   X(J+1)    1.0            X(J+2)    -1.0
 XN OBJ2(I)   X(J+2)    1.0            X(J+3)    -1.0
 XN OBJ3(I)   X(J+3)    1.0            X(J+4)    -1.0
 XN OBJ4(I)   X(J+4)    1.0            X(J+5)    -1.0
 ND

 DO K         1                        NC
 DI K         3
 IA K+1       K         1
 IA K+2       K         2
 IA K+3       K         3
 IA K+4       K         4
 XL C(K)      X(K)      1.0
 XL C(K+1)    X(K+1)    1.0            X(K+3)    1.0
 XL C(K+2)
 ND

CONSTANTS

 DO K         1                        NC
 DI K         3
 IA K+1       K         1
 IA K+2       K         2
 X  RHS       C(K)      3.0
 X  RHS       C(K+1)    1.0
 X  RHS       C(K+2)    1.0
 ND

BOUNDS

 FR BND       'DEFAULT'

START POINT

 DO I         1                        N
 DI I         4
 XV LUKVLI12  X(I)      2.0
 ND

 DO I         2                        N
 DI I         4
 XV LUKVLI12  X(I)      1.5
 ND

 DO I         3                        N
 DI I         4
 XV LUKVLI12  X(I)      -1.0
 ND

 DO I         4                        N
 DI I         4
 XV LUKVLI12  X(I)      0.5
 ND

ELEMENT TYPE

 EV SQR       V
 EV PROD      V                        W

ELEMENT USES

 DO K         1                        NC
 DI K         3
 IA K+1       K         1
 IA K+2       K         2
 XT EA(K)     SQR
 ZV EA(K)     V                        X(K+1)
 XT EB(K)     SQR
 ZV EB(K)     V                        X(K+2)
 XT E(K+1)    SQR
 ZV E(K+1)    V                        X(K+2)
 XT E(K+2)    PROD
 ZV E(K+2)    V                        X(K)
 ZV E(K+2)    W                        X(K+4)
 ND

GROUP TYPE

 GV L2        ALPHA
 GV L4        ALPHA

GROUP USES

 DO I         1                        (N-1)/4
 XT OBJ1(I)   L2
 XT OBJ2(I)   L2
 XT OBJ3(I)   L4
 XT OBJ4(I)   L4
 ND

 DO K         1                        NC
 DI K         3
 IA K+1       K         1
 IA K+2       K         2
 XE C(K)      EA(K)                    EB(K)
 XE C(K+1)    E(K+1)
 XE C(K+2)    E(K+2)
 ND

OBJECT BOUND

 LO LUKVLI12            0.0

*   Solution

*LO SOLTN               0.0

ENDATA

***********************
* SET UP THE FUNCTION *
* AND RANGE ROUTINES  *
***********************

ELEMENTS      LUKVLI12

INDIVIDUALS

 T  SQR
 F                      V * V
 G  V                   2.0 * V
 H  V         V         2.0

 T  PROD
 F                      V * W
 G  V                   W
 G  W                   V
 H  V         W         1.0

ENDATA

*********************
* SET UP THE GROUPS *
* ROUTINE           *
*********************

GROUPS        LUKVLI12

INDIVIDUALS

 T  L2
 F                      ALPHA * ALPHA
 G                      ALPHA + ALPHA
 H                      2.0

 T  L4
 F                      ALPHA ** 4
 G                      4.0 * ALPHA ** 3
 H                      12.0 * ALPHA ** 2

ENDATA
