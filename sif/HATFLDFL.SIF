***************************
* SET UP THE INITIAL DATA *
***************************

NAME          HATFLDFL

*   Problem :
*   *********

*   Fletcher's variation of a test problem (HATFLDF) from the OPTIMA user manual.
*   Monotonic paths to the solution from the initial point move to infinity
*   and back

*   Source:
*   "The OPTIMA user manual (issue No.8, p. 47)",
*   Numerical Optimization Centre, Hatfield Polytechnic (UK), 1989.

*   SIF input: Ph. Toint, May 1990, mods Nick Gould, August 2008

*   Nonlinear least-squares variant

*   classification SUR2-AN-3-0

*   Constants

 IE 1                   1
 IE 3                   3

VARIABLES

    X1
    X2
    X3

GROUPS

 DO I         1                        3
 XN G(I)      X1        1.0
 ND

CONSTANTS

    HATFLDFL  G1        0.032
    HATFLDFL  G2        0.056
    HATFLDFL  G3        0.099

BOUNDS

 FR HATFLDFL  'DEFAULT'

START POINT

*  the nasty starting point

 XV HATFLDFL  X1        1.2
 XV HATFLDFL  X2        -1.2
 XV HATFLDFL  X3        0.98

*XV HATFLDFL  X1        1.0
*XV HATFLDFL  X2        1.0
*XV HATFLDFL  X3        1.0

ELEMENT TYPE

 EV XPEXP     X                        Y
 EP XPEXP     T

ELEMENT USES

 XT 'DEFAULT' XPEXP
 DO I         1                        3
 RI RI        I
 ZV A(I)      X                        X2
 ZV A(I)      Y                        X3
 ZP A(I)      T                        RI
 ND

GROUP TYPE

 GV L2        GVAR

GROUP USES

 XT 'DEFAULT' L2
 DO I         1                        3
 XE G(I)      A(I)
 ND

ENDATA

***********************
* SET UP THE FUNCTION *
* AND RANGE ROUTINES  *
***********************

ELEMENTS      HATFLDFL

TEMPORARIES

 I  IT
 R  EX
 M  EXP

INDIVIDUALS

 T  XPEXP
 A  IT                  T
 A  EX                  Y ** IT
 F                      X * EX
 G  X                   EX
 G  Y                   T * X * Y ** ( IT - 1 )
 H  X         Y         T * Y ** ( IT - 1 )
 H  Y         Y         T * ( T - 1.0 ) * X * Y ** ( IT - 2 )

ENDATA

*********************
* SET UP THE GROUPS *
* ROUTINE           *
*********************

GROUPS        HATFLDFL

INDIVIDUALS

*   Least-square root groups

 T  L2
 F                      GVAR * GVAR
 G                      GVAR + GVAR
 H                      2.0

ENDATA
