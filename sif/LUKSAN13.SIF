***************************
* SET UP THE INITIAL DATA *
***************************

NAME          LUKSAN13

*   Problem :
*   *********

*   Problem 13 (chained and modified HS48) in the paper

*     L. Luksan
*     Hybrid methods in large sparse nonlinear least squares
*     J. Optimization Theory & Applications 89(3) 575-595 (1996)

*   SIF input: Nick Gould, June 2017.

*   classification NOR2-AN-V-V

*  seed for dimensions

 IE S                   32

*  number of unknowns

 IM N         S         3
 IA N         N         2

*  number of equations

 IM M         S         7

*   Define useful parameters

 IE 1                   1

VARIABLES

 DO I         1                        N
 X  X(I)
 ND

GROUPS

 IE I                   1
 IE K                   1
 DO J         1                        S
 IA K+1       K         1
 IA K+2       K         2
 IA K+3       K         3
 IA K+4       K         4
 IA K+5       K         5
 IA K+6       K         6
 IA I+1       I         1
 IA I+2       I         2
 IA I+3       I         3
 XE E(K)      X(I+1)    -10.0D0
 XE E(K+1)    X(I+2)    -10.0D0
 XE E(K+2)
 XE E(K+3)
 XE E(K+4)    X(I)      1.0D0          X(I+2)    1.0D0
 XE E(K+5)    X(I+1)    1.0D0          X(I+3)    1.0D0
 XE E(K+6)
 IA I         I         3
 IA K         K         7
 ND

CONSTANTS

 IE K                   1
 DO J         1                        S
 IA K+4       K         4
 IA K+5       K         5
 IA K+6       K         6
 X  LUKSAN13  E(K+4)    30.0
 X  LUKSAN13  E(K+5)    10.0
 X  LUKSAN13  E(K+6)    10.0
 IA K         K         7
 ND

BOUNDS

 FR LUKSAN13  'DEFAULT'

START POINT

 DO I         1                        N
 XV LUKSAN13  X(I)       -1.0
 ND

ELEMENT TYPE

 EV SQR       X
 EV SQRDIF    X1
 EV SQRDIF    X2
 IV SQRDIF    X
 EV PROD      X1
 EV PROD      X2

ELEMENT USES

 IE I                   1
 IE K                   1
 DO J         1                        S
 IA K+1       K         1
 IA K+2       K         2
 IA K+3       K         3
 IA K+4       K         4
 IA K+5       K         5
 IA K+6       K         6
 IA I+1       I         1
 IA I+2       I         2
 IA I+3       I         3
 IA I+4       I         4
 XT E(K)      SQR
 ZV E(K)      X                        X(I)
 XT E(K+1)    SQR
 ZV E(K+1)    X                        X(I+1)
 XT E(K+2)    SQRDIF
 ZV E(K+2)    X1                       X(I+2)
 ZV E(K+2)    X2                       X(I+3)
 XT E(K+3)    SQRDIF
 ZV E(K+3)    X1                       X(I+3)
 ZV E(K+3)    X2                       X(I+4)
 XT E(K+5)    SQR
 ZV E(K+5)    X                        X(I+2)
 XT E(K+6)    PROD
 ZV E(K+6)    X1                       X(I)
 ZV E(K+6)    X2                       X(I+4)
 IA I         I         3
 IA K         K         7
 ND

GROUP USES

 IE K                   1
 DO J         1                        S
 IA K+1       K         1
 IA K+2       K         2
 IA K+3       K         3
 IA K+4       K         4
 IA K+5       K         5
 IA K+6       K         6
 XE E(K)      E(K)      10.0
 XE E(K+1)    E(K+1)    10.0
 XE E(K+2)    E(K+2)
 XE E(K+3)    E(K+3)
 XE E(K+4)    E(K+1)
 XE E(K+5)    E(K+5)    -1.0
 XE E(K+6)    E(K+6)
 IA K         K         7
 ND

OBJECT BOUND

 LO LUKSAN13             0.0

*   Solution

*LO SOLTN                0.0

ENDATA

***********************
* SET UP THE FUNCTION *
* AND RANGE ROUTINES  *
***********************

ELEMENTS      LUKSAN13

INDIVIDUALS

 T  SQR
 F                      X * X
 G  X                   X + X
 H  X         X         2.0D0

 T  SQRDIF
 R  X         X1        1.0            X2        -1.0
 F                      X * X
 G  X                   X + X
 H  X         X         2.0D0

 T  PROD
 F                      X1 * X2
 G  X1                  X2
 G  X2                  X1
 H  X1        X2        1.0D0

ENDATA
