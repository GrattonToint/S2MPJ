***************************
* SET UP THE INITIAL DATA *
***************************

NAME          BDQRTIC

*   Problem :
*   *********
*   This problem is quartic and has a banded Hessian with bandwidth = 9

*   Source: Problem 61 in
*   A.R. Conn, N.I.M. Gould, M. Lescrenier and Ph.L. Toint,
*   "Performance of a multifrontal scheme for partially separable
*   optimization",
*   Report 88/4, Dept of Mathematics, FUNDP (Namur, B), 1988.

*   SIF input: Ph. Toint, Dec 1989.

*   classification SUR2-AN-V-0

*   Number of variables (variable)

*IE N                   100            $-PARAMETER     original value
*IE N                   500            $-PARAMETER
*IE N                   1000           $-PARAMETER
*IE N                   5000           $-PARAMETER
 IE N                   10             $-PARAMETER     modified for S2X tests

*   Number of groups

 IA N-4       N         -4

*   Define useful parameters

 IE 1                   1
 IE 2                   2
 IA N+1       N         1

VARIABLES

 DO I         1                        N
 X  X(I)
 ND

GROUPS

 DO I         1                        N-4
 XN L(I)      X(I)      -4.0
 XN G(I)
 ND

CONSTANTS

 DO I         1                        N-4
 X  BDQRTIC   L(I)      -3.0
 ND

BOUNDS

 FR BDQRTIC   'DEFAULT'

START POINT

 X  BDQRTIC   'DEFAULT' 1.0

ELEMENT TYPE

 EV SQ        EVAR

ELEMENT USES

 T  'DEFAULT' SQ

 DO I         1                        N
 ZV A(I)      EVAR                     X(I)
 ND

GROUP TYPE

 GV L2        GVAR

GROUP USES

 T  'DEFAULT' L2

 DO I         1                        N-4

 IA I+1       I          1
 IA I+2       I          2
 IA I+3       I          3

 XE G(I)      A(I)       1.0           A(I+1)    2.0
 XE G(I)      A(I+2)     3.0           A(I+3)    4.0
 XE G(I)      A(N)       5.0

 ND

OBJECT BOUND

 LO BDQRTIC              0.0

*   Solution

*LO SOLTN(100)           3.78769D+02
*LO SOLTN(500)           1.98101D+03
*LO SOLTN(1000)          3.98382D+03

ENDATA

***********************
* SET UP THE FUNCTION *
* AND RANGE ROUTINES  *
***********************

ELEMENTS      BDQRTIC

INDIVIDUALS

 T  SQ
 F                      EVAR * EVAR
 G  EVAR                EVAR + EVAR
 H  EVAR      EVAR      2.0

ENDATA

*********************
* SET UP THE GROUPS *
* ROUTINE           *
*********************

GROUPS        BDQRTIC

INDIVIDUALS

*   Least squares groups

 T  L2
 F                      GVAR * GVAR
 G                      GVAR + GVAR
 H                      2.0

ENDATA
