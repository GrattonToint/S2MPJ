***************************
* SET UP THE INITIAL DATA *
***************************

NAME          INDEF

*   Problem :
*   *********

*   A nonconvex problem which has an indefinite Hessian at
*   the starting point.

*   SIF input: Nick Gould, Oct 1992.

*   classification OUR2-AN-V-0

*   The number of variables is N.

*IE N                   10             $-PARAMETER     
*IE N                   50             $-PARAMETER
*IE N                   100            $-PARAMETER
*IE N                   1000           $-PARAMETER     original value
*IE N                   5000           $-PARAMETER
 IE N                   10             $-PARAMETER     modified for S2X tests

*   The parameter ALPHA controls the indefiniteness.
*   Larger values of ALPHA give more indefiniteness.

 RE ALPHA               0.5            $-PARAMETER     indefiniteness
*RE ALPHA               1.0            $-PARAMETER
*RE ALPHA               10.0           $-PARAMETER
*RE ALPHA               100.0          $-PARAMETER
*RE ALPHA               1000.0         $-PARAMETER

*   Other parameters

 IE 1                   1
 IE 2                   2
 IA N-1       N         -1
 IA N+1       N         1
 RI RN+1      N+1

VARIABLES

 DO I         1                        N
 X  X(I)
 ND

GROUPS

 DO I         1                        N
 XN L2(I)     X(I)      1.0
 ND

 DO I         2                        N-1
 XN COS(I)    X(I)      2.0            X(N)      -1.0
 XN COS(I)    X(1)      -1.0
 ND

BOUNDS

 FR INDEF     'DEFAULT' 

START POINT

*  start with X(I) = I/N+1.

 DO I         1                        N

 RI RI        I            
 R/ T         RI                       RN+1
 ZV INDEF1    X(I)                     T

 ND

 V  INDEF2    'DEFAULT' 1000.0

GROUP TYPE

 GV L2        GVAR
 GV COS       GVAR
 GP COS       ALPHA

GROUP USES

*T  'DEFAULT' L2

 DO I         2                        N-1
 XT COS(I)    COS
 ZP COS(I)    ALPHA                    ALPHA
 ND

OBJECT BOUND

*   Solution

*LO SOLTN               ??

ENDATA

*********************
* SET UP THE GROUPS *
* ROUTINE           *
*********************

GROUPS        INDEF

TEMPORARIES
 
*R  EXPG
*M  EXP
 M  COS
 M  SIN

INDIVIDUALS

 T  L2

 F                      GVAR * GVAR
 G                      GVAR + GVAR
 H                      2.0

*T  E2
*A  EXPG                EXP( GVAR * GVAR )
*F                      EXPG
*G                      2.0D+0 * GVAR * EXPG
*H                      ( 4.0D+0 * GVAR * GVAR + 2.0D+0 ) * EXPG

 T  COS

 F                      ALPHA * COS( GVAR )
 G                      - ALPHA * SIN( GVAR )
 H                      - ALPHA * COS( GVAR )

ENDATA

