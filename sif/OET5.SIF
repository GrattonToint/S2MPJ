***************************
* SET UP THE INITIAL DATA *
***************************

NAME          OET5

*   Problem :
*   *********

*   A nonlinear programming formulation of a discretization of
*   a nonlinear Chebychev problem.

*   The problem is

*       min  max || phi(x,w) ||, for all w in the interval I.
*        x    w

*   I is discretized, and the problem solved over the
*   discrete points.

*   Nonlinear programming formulation
*
*       min   u     s.t.  u - phi >= 0, u + phi >= 0
*       x,u

*   Specific problem: I = [0.25,1]
*   phi(x,w) = sqrt(w) - x4 - (x1 w^2 + x2 w + x3 )^2
 
*   Source: K. Oettershagen
*   "Ein superlinear knonvergenter algorithmus zur losung 
*    semi-infiniter optimierungsproblem",
*    Ph.D thesis, Bonn University, 1982

*   SIF input: Nick Gould, February, 1994.

*   classification LQR2-AN-5-V

*   Discretization

*IE M                   2
*IE M                   100
 IE M                   500

*   Define the interval

 RE LOWER               0.25
 RE UPPER               1.0

*   Define constants

 IE 0                   0
 IE 1                   1

 R- DIFF      UPPER                    LOWER
 RI RM        M
 R/ H         DIFF                     RM

VARIABLES

    U
    X1
    X2
    X3
    X4

GROUPS

 N  OBJ       U         1.0

 DO I         0                        M
 XG LO(I)     U         1.0            X4        -1.0
 XG UP(I)     U         1.0            X4        1.0
 ND

CONSTANTS

 DO I         0                        M
 RI RI        I
 R* W         RI                       H
 R+ W         W                        LOWER

 R( SQRTW     SQRT                     W
 RM -SQRTW    SQRTW     -1.0

 Z  OET5      LO(I)                    -SQRTW
 Z  OET5      UP(I)                    SQRTW

 ND

BOUNDS

 FR OET5      'DEFAULT'

START POINT

 V  OET5      'DEFAULT' 1.0

ELEMENT TYPE

 EV QUAD      X1                       X2
 EV QUAD      X3
 EP QUAD      W

ELEMENT USES

 DO I         0                        M
 RI RI        I
 R* W         RI                       H
 R+ W         W                        LOWER

 XT E(1,I)    QUAD
 ZV E(1,I)    X1                       X1
 ZV E(1,I)    X2                       X2
 ZV E(1,I)    X3                       X3
 ZP E(1,I)    W                        W

 ND

GROUP USES

 DO I         0                        M
 XE LO(I)     E(1,I)    
 XE UP(I)     E(1,I)    -1.0
 ND

ENDATA

***********************
* SET UP THE FUNCTION *
* AND RANGE ROUTINES  *
***********************

ELEMENTS      OET5

TEMPORARIES

 R  W2
 R  TERM

INDIVIDUALS

 T  QUAD

 A  W2                  W * W
 A  TERM                W2 * X1 + W * X2 + X3                

 F                      TERM * TERM

 G  X1                  2.0 * TERM * W2
 G  X2                  2.0 * TERM * W
 G  X3                  2.0 * TERM

 H  X1        X1        2.0 * W2 * W2
 H  X2        X1        2.0 * W2 * W
 H  X3        X1        2.0 * W2
 H  X2        X2        2.0 * W2
 H  X3        X2        2.0 * W
 H  X3        X3        2.0

ENDATA

