***************************
* SET UP THE INITIAL DATA *
***************************

NAME          SINQUAD2

*   Problem :
*   *********

*   Another function with nontrivial groups and
*   repetitious elements.

*   Source:
*   N. Gould, private communication.

*   SIF input: N. Gould, Dec 1989.
*   modifield version of SINQUAD (formulation corrected) May 2024

*   classification OUR2-AY-V-0

*   number of variables

*IE N                   5              $-PARAMETER     original value
*IE N                   10             $-PARAMETER
 IE N                   10             $-PARAMETER     modified for S2X tests
*IE N                   50             $-PARAMETER
*IE N                   100            $-PARAMETER
*IE N                   500            $-PARAMETER
*IE N                   1000           $-PARAMETER
*IE N                   5000           $-PARAMETER
*IE N                   10000          $-PARAMETER

*   other parameter definitions

 IE 1                   1
 IE 2                   2
 IA NM1       N         -1

VARIABLES

 DO I         1                        N
 X  X(I)
 ND

GROUPS

*  objective function (1st group)

 N  G1        X1        1.0

*  objective function (remaining groups)

 DO I         2                        N
 XN G(I)
 OD

CONSTANTS

    SINQUAD   G1        1.0

BOUNDS

 FR SINQUAD   'DEFAULT'

START POINT

 XV SINQUAD   'DEFAULT' 0.1

ELEMENT TYPE

 EV SQ        V1
 EV SINE      V1                       V2
 IV SINE      U1

ELEMENT USES

 T  E1        SQ
 ZV E1        V1                       X1

 DO I         2                        N

 XT E(I)      SQ
 ZV E(I)      V1                       X(I)

 ND

 DO I         2                        NM1

 XT S(I)      SINE
 ZV S(I)      V1                       X(I)
 ZV S(I)      V2                       X(N)

 ND

GROUP TYPE

 GV L2        GVAR
 GV L4        GVAR

GROUP USES

 T  G1        L4

 DO I         2                        N
 XT G(I)      L2
 XE G(I)      E(I)                     E(1)      -1.0
 ND

 DO I         2                        NM1
 XE G(I)      S(I)
 ND

OBJECT BOUND

 LO SINQUAD             0.0

*   Solution

*LO SOLTN               -3.0

ENDATA

***********************
* SET UP THE FUNCTION *
* AND RANGE ROUTINES  *
***********************

ELEMENTS      SINQUAD2

TEMPORARIES

 M  SIN
 M  COS

INDIVIDUALS

 T  SQ
 F                      V1 * V1
 G  V1                  V1 + V1
 H  V1        V1        2.0

 T  SINE
 R  U1        V1        1.0            V2        -1.0
 F                      SIN( U1 )
 G  U1                  COS( U1 )
 H  U1        U1        - SIN( U1 )

ENDATA

*********************
* SET UP THE GROUPS *
* ROUTINE           *
*********************

GROUPS        SINQUAD2

INDIVIDUALS

 T  L2
 F                      GVAR * GVAR
 G                      GVAR + GVAR
 H                      2.0

 T  L4
 F                      GVAR ** 4
 G                      4.0 * GVAR ** 3
 H                      12.0 * GVAR ** 2

ENDATA
