***************************
* SET UP THE INITIAL DATA *
***************************

NAME          LEVYMONT9

*   Problem :
*   *********
*   A global optimization example due to Levy & Montalvo 
*   This problem is one of the parameterised set LEVYMONT5-LEVYMONT10

*   Source:  problem 9 in

*   A. V. Levy and A. Montalvo
*   "The Tunneling Algorithm for the Global Minimization of Functions"
*   SIAM J. Sci. Stat. Comp. 6(1) 1985 15:29 
*   https://doi.org/10.1137/0906002

*   SIF input: Nick Gould, August 2021

*   classification SBR2-AY-8-0

*   N is the number of variables

*IE N                   8              $-PARAMETER
 IE N                   8              $-PARAMETER     modified for S2X tests

*  problem parameters

 RE A                   1.0
 RE K                   10.0

 RE L                   1.0
 RE C                   0.0

*   other useful parameters

 IE 1                   1
 IE 2                   2

 RF PI/4      ARCTAN    1.0
 RM PI        PI/4      4.0

 RI RN        N
 R- A-C       A                        C

 R/ PI/N      PI                       RN
 R* KPI/N     K                        PI/N
 R( ROOTKPI/N SQRT                     KPI/N

 R/ N/PI      RN                       PI
 R/ N/KPI     N/PI                     K

VARIABLES

 DO I         1                        N
 X  X(I)
 ND

GROUPS

 DO I         1                        N
 ZN Q(I)      X(I)                     L
 ZN Q(I)      'SCALE'                  N/PI
 ZN N(I)
 ND

CONSTANTS

 DO I         1                        N
 Z  LEVYMONT  Q(I)                     A-C
 ND

BOUNDS

 LO LEVYMONT  'DEFAULT' -10.0
 UP LEVYMONT  'DEFAULT' 10.0

START POINT

 XV LEVYMONTA 'DEFAULT' 8.0
 XV LEVYMONTA X1        -8.0
 XV LEVYMONTA X2        8.0

 XV LEVYMONTB 'DEFAULT' 8.0

 XV LEVYMONTC 'DEFAULT' 5.0
 XV LEVYMONTC X1        -5.0
 XV LEVYMONTC X2        5.0

 XV LEVYMONTD 'DEFAULT' -8.0

ELEMENT TYPE

 EV S2        X
 EP S2        L
 EP S2        C
 EV PS2       X
 EV PS2       Z
 EP PS2       L
 EP PS2       C
 EP PS2       A

ELEMENT USES

 XT E(1)      S2
 ZV E(1)      X                        X(1)
 ZP E(1)      L                        L
 ZP E(1)      C                        C

 DO I         2                        N
 I- I-1       I                        1
 XT E(I)      PS2
 ZV E(I)      X                        X(I)
 ZV E(I)      Z                        X(I-1)
 ZP E(I)      L                        L
 ZP E(I)      C                        C
 ZP E(I)      A                        A
 ND

GROUP TYPE

 GV L2        GVAR

GROUP USES

 DO I         1                        N
 XT Q(I)      L2
 XT N(I)      L2
 ZE N(I)      E(I)                     ROOTKPI/N
 ND

OBJECT BOUND

 LO LEVYMONT            0.0

*   Solution

*LO SOLTN               0.0

ENDATA

***********************
* SET UP THE FUNCTION *
* AND RANGE ROUTINES  *
***********************

ELEMENTS      LEVYMONT9

TEMPORARIES

 R  PI
 R  PIL
 R  U
 R  V
 R  SINV
 R  COSV
 M  SIN
 M  COS
 M  ATAN

GLOBALS

 A  PI                  4.0 * ATAN( 1.0D0 )

INDIVIDUALS

*   sin( pi( l x + c ) ) = sin v where v = pi( l x + c ) )

 T  S2
 A  PIL                 PI * L
 A  V                   PIL * X + PI * C
 A  SINV                SIN( V )
 A  COSV                COS( V )
 F                      SINV
 G  X                   PIL * COSV
 H  X         X         - PIL * PIL * SINV

*   (l z + c - a) * sin( pi( l x + c ) ) = u sin v 
*   where u = l z + c - a and v = pi( l x + c ) )

 T  PS2
 A  PIL                 PI * L
 A  U                   L * Z + C - A
 A  V                   PIL * X + PI * C
 A  SINV                SIN( V )
 A  COSV                COS( V )
 F                      U * SINV
 G  X                   PIL * U * COSV
 G  Z                   L * SINV
 H  X         X         - PIL * PIL * U * SINV
 H  X         Z         L * PIL * COSV
 H  Z         Z         0.0

ENDATA

*********************
* SET UP THE GROUPS *
* ROUTINE           *
*********************

GROUPS        LEVYMONT9

INDIVIDUALS

 T  L2
 F                      GVAR * GVAR
 G                      2.0 * GVAR
 H                      2.0

ENDATA
