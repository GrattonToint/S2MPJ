***************************
* SET UP THE INITIAL DATA *
***************************

NAME          ERRINRSM

*   Problem :
*   --------

*   A variable dimension version of an incorrect version of the
*   chained Rosenbrock function (ERRINROS) by Luksan et al.

*   Source: problem 28 in
*   L. Luksan, C. Matonoha and J. Vlcek  
*   Modified CUTE problems for sparse unconstraoined optimization
*   Technical Report 1081
*   Institute of Computer Science
*   Academy of Science of the Czech Republic

*   SIF input: Ph. Toint, Sept 1990.
*              this version Nick Gould, June, 2013

*   classification SUR2-AN-V-0

*   Number of variables (at most 50)

*IE N                   10             $-PARAMETER
*IE N                   25             $-PARAMETER
*IE N                   50             $-PARAMETER
 IE N                   10             $-PARAMETER     modified for S2X tests

*   other parameter definitions

 IE 1                   1
 IE 2                   2

VARIABLES

 DO I         1                        N
 X  X(I)
 ND

GROUPS

 DO I         2                        N
 IA I-1       I         -1
 XN SQ(I)     X(I-1)    1.0
 XN B(I)      X(I)      1.0
 ND

CONSTANTS

 DO I         2                        N
 X  ERRINRSM  B(I)      1.0
 ND

BOUNDS

 FR ERRINRSM  'DEFAULT'

START POINT

 DO I         1                        N
 X  ERRINRSM  X(I)      -1.0
 ND

ELEMENT TYPE

 EV ETYPE     V1

ELEMENT USES

 XT 'DEFAULT' ETYPE

 DO I         2                        N
 ZV ELA(I)    V1                       X(I)
 ND

GROUP TYPE

 GV L2        GVAR

GROUP USES

 XT 'DEFAULT' L2

 DO I         2                        N
 RI RI        I
 R( SINI      SIN                      RI
 RA ALPHA     SINI       1.5
 R* AI2       ALPHA                    ALPHA
 RM AI        AI2        16.0
 ZE SQ(I)     ELA(I)                   AI
 ND

OBJECT BOUND

 LO ERRINRSM             0.0

*   Solution

*LO SOLTN(10)            6.69463214
*LO SOLTN(25)            18.4609060
*LO SOLTN(50)            39.9041540

ENDATA

***********************
* SET UP THE FUNCTION *
* AND RANGE ROUTINES  *
***********************

ELEMENTS      ERRINRSM

INDIVIDUALS

 T  ETYPE
 F                      - V1 ** 2
 G  V1                  - 2.0 * V1
 H  V1        V1        - 2.0

ENDATA

*********************
* SET UP THE GROUPS *
* ROUTINE           *
*********************

GROUPS        ERRINRSM

INDIVIDUALS

 T  L2

 F                      GVAR * GVAR
 G                      GVAR + GVAR
 H                      2.0

ENDATA
